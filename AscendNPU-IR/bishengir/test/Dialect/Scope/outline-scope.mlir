// RUN: bishengir-opt -outline-scope -split-input-file %s | FileCheck %s

// CHECK-LABEL: func.func @test_scope_scope_scope_0(
// CHECK-SAME: %[[F0_CST:.*]]: f32,
// CHECK-SAME: %[[F0_ALLOC:.*]]: memref<f32>) attributes {debug = 15 : index, tcore_type = #hivm.tcore_type<VECTOR>} {
// CHECK: memref.store %[[F0_CST]], %[[F0_ALLOC]][] {debug = 16 : index} : memref<f32>

// CHECK-LABEL: func.func @test_scope_scope_scope_1(
// CHECK-SAME: %[[F1_CST:.*]]: f32,
// CHECK-SAME: %[[F1_ALLOC:.*]]: memref<f32>) attributes {debug = 11 : index} {
// CHECK: memref.store %[[F1_CST]], %[[F1_ALLOC]][] {debug = 12 : index} : memref<f32>

// CHECK: func.func @test_scope_scope_scope_2(%[[F2_CST_1:.*]]: f32, %[[F2_ALLOC:.*]]: memref<f32>, %[[F2_IDX_A:.*]]: index, %[[F2_IDX_B:.*]]: index, %[[F2_STEP:.*]]: index, %[[F2_CST_2:.*]]: f32) attributes {debug = 2 : index, tcore_type = #hivm.tcore_type<CUBE>} {
// CHECK: memref.store %[[F2_CST_1]], %[[F2_ALLOC]][] {debug = 3 : index} : memref<f32>
// CHECK: scf.for %[[VAL_6:.*]] = %[[F2_IDX_A]] to %[[F2_IDX_B]] step %[[F2_STEP]] {
// CHECK: memref.store %[[F2_CST_1]], %[[F2_ALLOC]][] {debug = 9 : index} : memref<f32>
// CHECK: } {debug = 8 : index}
// CHECK: call @test_scope_scope_scope_1(%[[F2_CST_2]], %[[F2_ALLOC]]) : (f32, memref<f32>) -> ()

// CHECK-LABEL: func.func @test_scope_scope(
// CHECK-SAME: %[[ALLOC_0:.*]]: memref<f32>) attributes {debug = 0 : index} {
// CHECK: %[[STEP:.*]] = arith.constant {debug = 7 : index} 1 : index
// CHECK: %[[IDX_B:.*]] = arith.constant {debug = 6 : index} 3 : index
// CHECK: %[[IDX_A:.*]] = arith.constant {debug = 5 : index} 0 : index
// CHECK: %[[CST_2:.*]] = arith.constant {debug = 4 : index} 2.000000e-01 : f32
// CHECK: %[[CST_1:.*]] = arith.constant {debug = 1 : index} 1.000000e-01 : f32
// CHECK: call @test_scope_scope_scope_2(%[[CST_1]], %[[ALLOC_0]], %[[IDX_A]], %[[IDX_B]], %[[STEP]], %[[CST_2]]) : (f32, memref<f32>, index, index, index, f32) -> ()
// CHECK: call @test_scope_scope_scope_0(%[[CST_1]], %[[ALLOC_0]]) : (f32, memref<f32>) -> ()
// CHECK: return {debug = 18 : index}

module {
  func.func @test_scope_scope(%arg0: memref<f32>) attributes {debug = 0 : index} {
    %cst = arith.constant {debug = 1 : index} 1.000000e-01 : f32
    scope.scope : () -> () {
      memref.store %cst, %arg0[] {debug = 3 : index} : memref<f32>
      %cst_0 = arith.constant {debug = 4 : index} 2.000000e-01 : f32
      %c0 = arith.constant {debug = 5 : index} 0 : index
      %c3 = arith.constant {debug = 6 : index} 3 : index
      %c1 = arith.constant {debug = 7 : index} 1 : index
      scf.for %arg1 = %c0 to %c3 step %c1 {
        memref.store %cst, %arg0[] {debug = 9 : index} : memref<f32>
      } {debug = 8 : index}
      scope.scope : () -> () {
        memref.store %cst_0, %arg0[] {debug = 12 : index} : memref<f32>
        scope.return {debug = 13 : index}
      } {debug = 11 : index}
      scope.return {debug = 14 : index}
    } {debug = 2 : index, tcore_type = #hivm.tcore_type<CUBE>}
    scope.scope : () -> () {
      memref.store %cst, %arg0[] {debug = 16 : index} : memref<f32>
      scope.return {debug = 17 : index}
    } {debug = 15 : index, tcore_type = #hivm.tcore_type<VECTOR>}
    return {debug = 18 : index}
  }
}

// -----

// CHECK: func.func @test_scope_with_yields_scope_0(%[[ARG_0:.*]]: f32, %[[ARG_1:.*]]: f32) -> (f32, f32, f32) {
// CHECK: return %[[ARG_0]], %[[ARG_1]], %[[ARG_0]] : f32, f32, f32
// CHECK-LABEL: func.func @test_scope_with_yields(
// CHECK: %[[CST_0:.*]] = arith.constant 0.000000e+00 : f32
// CHECK: %[[CST_1:.*]] = arith.constant 1.000000e+00 : f32
// CHECK: %[[CALL:.*]]:3 = call @test_scope_with_yields_scope_0(%[[CST_0]], %[[CST_1]]) : (f32, f32) -> (f32, f32, f32)
// CHECK: return %[[CALL]]#0, %[[CALL]]#1, %[[CALL]]#2 : f32, f32, f32
module{
  func.func @test_scope_with_yields() -> (f32, f32, f32){
    %cst = arith.constant 0.000000e+00 : f32
    %cst_1 = arith.constant 1.000000e+00 : f32
    %0:3 = scope.scope : () -> (f32, f32, f32) {
      scope.return %cst, %cst_1, %cst : f32, f32, f32
    }
    return %0#0, %0#1, %0#2 : f32, f32, f32
  }
}

// -----

// CHECK: func.func @test_scope_with_outline_attr
// CHECK: %[[CST_0:.*]] = arith.constant 0.000000e+00 : f32
// CHECK: scope.scope
// CHECK: scope.return %[[CST_0]]
// CHECK: call @test_scope_with_outline_attr_scope_0()
module attributes {hacc.target = #hacc.target<"Ascend950PR_957b">} {
  func.func @test_scope_with_outline_attr() -> (f32, f32){
    %cst = arith.constant 0.000000e+00 : f32
    %cst_1 = arith.constant 1.000000e+00 : f32
    %0 = scope.scope : () -> (f32) {
      scope.return %cst : f32
    }
    %1 = scope.scope : () -> (f32) {
      scope.return %cst_1 : f32
    } {outline = true}
    return %0, %1 : f32, f32
  }
}

// -----

// CHECK-LABEL: func.func @outline_varange_constants_scope_0() -> tensor<8xi32>
// CHECK: %[[C0:.*]] = arith.constant 0 : index
// CHECK: %[[C1:.*]] = arith.constant 1 : index
// CHECK: %[[EMPTY:.*]] = tensor.empty() : tensor<8xi32>
// CHECK: %[[ARANGE:.*]] = hivm.hir.varange offset[%[[C0]]] strides[%[[C1]]] outs(%[[EMPTY]] : tensor<8xi32>) -> tensor<8xi32>
// CHECK: return %[[ARANGE]] : tensor<8xi32>
// CHECK-LABEL: func.func @outline_varange_constants() -> tensor<8xi32>
// CHECK: %[[CALL:.*]] = call @outline_varange_constants_scope_0() : () -> tensor<8xi32>
// CHECK: return %[[CALL]] : tensor<8xi32>
module attributes {hacc.target = #hacc.target<"Ascend950PR_957b">} {
  func.func @outline_varange_constants() -> tensor<8xi32> {
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 1 : index
    %0 = scope.scope : () -> tensor<8xi32> {
      %empty = tensor.empty() : tensor<8xi32>
      %arange = hivm.hir.varange offset[%c0] strides[%c1] outs(%empty : tensor<8xi32>) -> tensor<8xi32>
      scope.return %arange : tensor<8xi32>
    } {outline = true}
    return %0 : tensor<8xi32>
  }
}

// CHECK-LABEL: func.func @test_mixed_inputs_scope_0(
// CHECK-SAME: %[[CST:.*]]: f32, %[[REINTERPRET1:.*]]: memref<8xf32, strided<[1], offset: ?>>, %[[C0:.*]]: index,
// CHECK-SAME: %[[REINTERPRET2:.*]]: memref<8xf32, strided<[1], offset: 2>>) attributes {outline = true}
// CHECK: memref.store %[[CST]], %[[REINTERPRET1]][%[[C0]]] : memref<8xf32, strided<[1], offset: ?>>
// CHECK: memref.store %[[CST]], %[[REINTERPRET2]][%[[C0]]] : memref<8xf32, strided<[1], offset: 2>>
// CHECK: return

// CHECK-LABEL: func.func @test_mixed_inputs(
// CHECK: %[[C0:.*]] = arith.constant 0 : index
// CHECK: %[[CST:.*]] = arith.constant 1.000000e+00 : f32
// CHECK: %[[C4:.*]] = arith.constant 4 : index
// CHECK: %[[REINTERPRET1:.*]] = memref.reinterpret_cast %[[ARG0:.*]] to offset: [%[[C4]]]
// CHECK: %[[REINTERPRET2:.*]] = memref.reinterpret_cast %[[ARG1:.*]] to offset: [2]
// CHECK: call @test_mixed_inputs_scope_0(%[[CST]], %[[REINTERPRET1]], %[[C0]], %[[REINTERPRET2]])
module {
  func.func @test_mixed_inputs(%arg0: memref<?xf32>, %arg1: memref<?xf32>) {
    %c4 = arith.constant 4 : index
    %reinterpret1 = memref.reinterpret_cast %arg0 to offset: [%c4], sizes: [8], strides: [1] : memref<?xf32> to memref<8xf32, strided<[1], offset: ?>>
    %reinterpret2 = memref.reinterpret_cast %arg1 to offset: [2], sizes: [8], strides: [1] : memref<?xf32> to memref<8xf32, strided<[1], offset: 2>>
    %cst = arith.constant 1.0 : f32
    %c0 = arith.constant 0 : index
    scope.scope : () -> () {
      memref.store %cst, %reinterpret1[%c0] : memref<8xf32, strided<[1], offset: ?>>
      memref.store %cst, %reinterpret2[%c0] : memref<8xf32, strided<[1], offset: 2>>
      scope.return
    } {outline = true}
    return
  }
}

// -----

// Test: SIMT scope with sub-block tiling reverted attribute should be wrapped
// in scf.if guard with limit_sub_block_id0.
// CHECK-LABEL: func.func @test_simt_scope_with_reverted_attr_scope_0(
// CHECK: memref.store
// CHECK: return

// CHECK-LABEL: func.func @test_simt_scope_with_reverted_attr(
// CHECK: hivm.hir.get_sub_block_idx
// CHECK: arith.index_cast
// CHECK: arith.cmpi eq
// CHECK: scf.if
// CHECK: call @test_simt_scope_with_reverted_attr_scope_0
// CHECK: limit_sub_block_id0
module attributes {"hivm.tile_and_bind_subblock_reverted"} {
  func.func @test_simt_scope_with_reverted_attr(%arg0: memref<f32>) {
    %cst = arith.constant 1.0 : f32
    scope.scope : () -> () {
      memref.store %cst, %arg0[] : memref<f32>
      scope.return
    } {hivm.vf_mode = #hivm.vf_mode<SIMT>}
    return
  }
}

// -----

// Test: SIMT scope without the reverted attribute should produce a plain call
// (no scf.if guard).
// CHECK-LABEL: func.func @test_simt_scope_without_reverted_attr_scope_0(
// CHECK: memref.store
// CHECK: return

// CHECK-LABEL: func.func @test_simt_scope_without_reverted_attr(
// CHECK-NOT: hivm.hir.get_sub_block_idx
// CHECK: call @test_simt_scope_without_reverted_attr_scope_0
module {
  func.func @test_simt_scope_without_reverted_attr(%arg0: memref<f32>) {
    %cst = arith.constant 1.0 : f32
    scope.scope : () -> () {
      memref.store %cst, %arg0[] : memref<f32>
      scope.return
    } {hivm.vf_mode = #hivm.vf_mode<SIMT>}
    return
  }
}

// -----

// Test: Non-SIMT scope with the reverted attribute should produce a plain call
// (no scf.if guard) because both conditions (SIMT AND reverted) must hold.
// CHECK-LABEL: func.func @test_non_simt_scope_with_reverted_attr_scope_0(
// CHECK: memref.store
// CHECK: return

// CHECK-LABEL: func.func @test_non_simt_scope_with_reverted_attr(
// CHECK-NOT: hivm.hir.get_sub_block_idx
// CHECK: call @test_non_simt_scope_with_reverted_attr_scope_0
module attributes {"hivm.tile_and_bind_subblock_reverted"} {
  func.func @test_non_simt_scope_with_reverted_attr(%arg0: memref<f32>) {
    %cst = arith.constant 1.0 : f32
    scope.scope : () -> () {
      memref.store %cst, %arg0[] : memref<f32>
      scope.return
    }
    return
  }
}
