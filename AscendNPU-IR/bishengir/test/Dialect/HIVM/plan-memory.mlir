// RUN: bishengir-opt %s -hacc-append-device-spec=target=Ascend910B1 -hivm-plan-memory -split-input-file -verify-diagnostics | FileCheck %s
// RUN: bishengir-opt %s -hacc-append-device-spec=target=Ascend910B1 -hivm-plan-memory=plan-memory-strategy=largest-first -split-input-file -verify-diagnostics | FileCheck %s --check-prefix=CHECK-REORDER

module {
  // CHECK-LABEL: func.func @test_mem_allocate_basic
  func.func @test_mem_allocate_basic(%src : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                    %dst3 : memref<16x16x16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
    %copy_in_ub = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
    %dst1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
    %dst2 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
    %copy_out_ub = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%src : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%copy_in_ub : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.vadd ins(%copy_in_ub, %copy_in_ub : memref<16x16x16xf16, #hivm.address_space<ub>>,
                      memref<16x16x16xf16, #hivm.address_space<ub>>)
                  outs(%dst1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.vadd ins(%dst1, %dst1 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                      memref<16x16x16xf16, #hivm.address_space<ub>>)
                  outs(%dst2 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.vadd ins(%dst2, %dst2 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                      memref<16x16x16xf16, #hivm.address_space<ub>>)
                  outs(%copy_out_ub : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.store ins(%copy_out_ub : memref<16x16x16xf16,#hivm.address_space<ub>>)
                   outs(%dst3: memref<16x16x16xf16,#hivm.address_space<gm>>)
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_noreuse_max
  func.func @test_mem_noreuse_max(%src : memref<16384xi64, #hivm.address_space<gm>>,
                                  %dst : memref<16384xi32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0:.*]])
    %alloc = memref.alloc() : memref<16384xi64, #hivm.address_space<ub>>
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST1:.*]])
    %alloc_0 = memref.alloc() : memref<16384xi32, #hivm.address_space<ub>>
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST2:.*]])
    %alloc_1 = memref.alloc() : memref<0xi64, #hivm.address_space<ub>>
    hivm.hir.load ins(%src : memref<16384xi64, #hivm.address_space<gm>>)
                  outs(%alloc : memref<16384xi64, #hivm.address_space<ub>>)
    hivm.hir.vcast ins(%alloc : memref<16384xi64, #hivm.address_space<ub>>)
                  outs(%alloc_0 : memref<16384xi32, #hivm.address_space<ub>>)
                  temp_buffer (%alloc_1 : memref<0xi64, #hivm.address_space<ub>>) round_mode = <truncwithoverflow>
    hivm.hir.store ins(%alloc_0 : memref<16384xi32,#hivm.address_space<ub>>)
                   outs(%dst: memref<16384xi32,#hivm.address_space<gm>>)
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_specalloc_max
  func.func @test_mem_specalloc_max(%src1 : memref<16384xi64, #hivm.address_space<gm>>,
                                    %src2 : memref<16384xi32, #hivm.address_space<gm>>,
                                    %dst : memref<16384xi32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST2:.*]] = arith.constant 131072 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST1:.*]])
    %alloc = memref.alloc() : memref<16384xi64, #hivm.address_space<ub>>
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST2]])
    %alloc_0 = memref.alloc() : memref<16384xi32, #hivm.address_space<ub>>
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
    %alloc_1 = memref.alloc() : memref<0xi64, #hivm.address_space<ub>>
    hivm.hir.load ins(%src1 : memref<16384xi64, #hivm.address_space<gm>>)
                  outs(%alloc : memref<16384xi64, #hivm.address_space<ub>>)
    hivm.hir.vcast ins(%alloc : memref<16384xi64, #hivm.address_space<ub>>)
                  outs(%alloc_0 : memref<16384xi32, #hivm.address_space<ub>>)
                  temp_buffer (%alloc_1 : memref<0xi64, #hivm.address_space<ub>>) round_mode = <truncwithoverflow>
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
    %alloc_2 = memref.alloc() : memref<16384xi32, #hivm.address_space<ub>>
    hivm.hir.load ins(%src2 : memref<16384xi32, #hivm.address_space<gm>>)
                  outs(%alloc_2 : memref<16384xi32, #hivm.address_space<ub>>)
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST2]])
    %alloc_3 = memref.alloc() : memref<16384xi32, #hivm.address_space<ub>>
    hivm.hir.vadd ins(%alloc_0, %alloc_2 : memref<16384xi32, #hivm.address_space<ub>>, memref<16384xi32, #hivm.address_space<ub>>)
                  outs(%alloc_3 : memref<16384xi32, #hivm.address_space<ub>>)
    hivm.hir.store ins(%alloc_3 : memref<16384xi32,#hivm.address_space<ub>>)
                   outs(%dst: memref<16384xi32,#hivm.address_space<gm>>)
    return
  }
}


// -----
module {
  // CHECK-LABEL: func.func @test_infer_mem_allocate_loop_conflict
  func.func @test_infer_mem_allocate_loop_conflict(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                                   %alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                                   %alloc6 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                                   %alloc8 : memref<16x16x16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %start = arith.constant 0 : index
    %end = arith.constant 1024 : index
    %step = arith.constant 128 : index
    scf.for %iv = %start to %end step %step {
      // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
      %alloc1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
      %alloc3 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
      %alloc5 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
      %alloc7 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                     outs(%alloc1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%alloc1, %alloc1 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16x16xf16, #hivm.address_space<ub>>)
                    outs(%alloc3 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.store ins(%alloc3 : memref<16x16x16xf16, #hivm.address_space<ub>>)
                     outs(%alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>)
      hivm.hir.load ins(%alloc6 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                    outs(%alloc5 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%alloc5, %alloc5 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16x16xf16, #hivm.address_space<ub>>)
                    outs(%alloc7 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.store ins(%alloc7 : memref<16x16x16xf16, #hivm.address_space<ub>>)
                     outs(%alloc8 : memref<16x16x16xf16, #hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_multi_buffer_loadstore_not_inplace
  func.func @test_multi_buffer_loadstore_not_inplace(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                                     %alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                                     %alloc6 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                                     %alloc8 : memref<16x16x16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST7:.*]] = arith.constant 57344 : i64
    // CHECK: %[[CONST3:.*]] = arith.constant 24576 : i64
    // CHECK: %[[CONST6:.*]] = arith.constant 49152 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 16384 : i64
    // CHECK: %[[CONST5:.*]] = arith.constant 40960 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST4:.*]] = arith.constant 32768 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %start = arith.constant 0 : index
    %end = arith.constant 1024 : index
    %step = arith.constant 128 : index
    scf.for %iv = %start to %end step %step {
      // CHECK: %[[ALLOC_0:.*]] = hivm.hir.pointer_cast(%[[CONST3]], %[[CONST7]])
      // CHECK: %[[ALLOC_1:.*]] = hivm.hir.pointer_cast(%[[CONST2]], %[[CONST6]])
      // CHECK: %[[ALLOC_2:.*]] = hivm.hir.pointer_cast(%[[CONST1]], %[[CONST5]])
      // CHECK: %[[ALLOC_3:.*]] = hivm.hir.pointer_cast(%[[CONST0]], %[[CONST4]])
      %alloc1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      annotation.mark %alloc1 {hivm.multi_buffer = 2 : i32} : memref<16x16x16xf16, #hivm.address_space<ub>>
      %alloc3 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      annotation.mark %alloc3 {hivm.multi_buffer = 2 : i32} : memref<16x16x16xf16, #hivm.address_space<ub>>
      %alloc5 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      annotation.mark %alloc5 {hivm.multi_buffer = 2 : i32} : memref<16x16x16xf16, #hivm.address_space<ub>>
      %alloc7 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      annotation.mark %alloc7 {hivm.multi_buffer = 2 : i32} : memref<16x16x16xf16, #hivm.address_space<ub>>
      // CHECK: hivm.hir.load ins({{.*}}) outs(%[[ALLOC_3]] : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.load ins(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                     outs(%alloc1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.vadd ins(%[[ALLOC_3]], %[[ALLOC_3]] : memref<16x16x16xf16, #hivm.address_space<ub>>, memref<16x16x16xf16, #hivm.address_space<ub>>) outs(%[[ALLOC_2]] : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%alloc1, %alloc1 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16x16xf16, #hivm.address_space<ub>>)
                    outs(%alloc3 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.store ins(%[[ALLOC_2]] : memref<16x16x16xf16, #hivm.address_space<ub>>) outs({{.*}})
      hivm.hir.store ins(%alloc3 : memref<16x16x16xf16, #hivm.address_space<ub>>)
                     outs(%alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>)
      // CHECK: hivm.hir.load ins({{.*}}) outs(%[[ALLOC_1]] : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.load ins(%alloc6 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                    outs(%alloc5 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.vadd ins(%[[ALLOC_1]], %[[ALLOC_1]] : memref<16x16x16xf16, #hivm.address_space<ub>>, memref<16x16x16xf16, #hivm.address_space<ub>>) outs(%[[ALLOC_0]] : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%alloc5, %alloc5 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16x16xf16, #hivm.address_space<ub>>)
                    outs(%alloc7 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.store ins(%[[ALLOC_0]] : memref<16x16x16xf16, #hivm.address_space<ub>>) outs({{.*}})
      hivm.hir.store ins(%alloc7 : memref<16x16x16xf16, #hivm.address_space<ub>>)
                     outs(%alloc8 : memref<16x16x16xf16, #hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_multi_buffer_loadstore_level0_inplace
  func.func @test_multi_buffer_loadstore_level0_inplace(%arg0 : memref<24576xf32, #hivm.address_space<gm>>,
                                                        %arg1 : memref<24576xf32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 98304 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %start = arith.constant 0 : index
    %end = arith.constant 1024 : index
    %step = arith.constant 128 : index
    scf.for %iv = %start to %end step %step {
      // CHECK: %[[ALLOC_0:.*]] = hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]])
      // CHECK: %[[ALLOC_1:.*]] = hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]])
      %alloc_0 = memref.alloc() : memref<24576xf32, #hivm.address_space<ub>>
      annotation.mark %alloc_0 {hivm.multi_buffer = 2 : i32} : memref<24576xf32, #hivm.address_space<ub>>
      %alloc_1 = memref.alloc() : memref<24576xf32, #hivm.address_space<ub>>
      annotation.mark %alloc_1 {hivm.multi_buffer = 2 : i32} : memref<24576xf32, #hivm.address_space<ub>>
      // CHECK: hivm.hir.load ins({{.*}}) outs(%[[ALLOC_1]] : memref<24576xf32, #hivm.address_space<ub>>)
      hivm.hir.load ins(%arg0 : memref<24576xf32, #hivm.address_space<gm>>)
                     outs(%alloc_0 : memref<24576xf32, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.vadd ins(%[[ALLOC_1]], %[[ALLOC_1]] : memref<24576xf32, #hivm.address_space<ub>>, memref<24576xf32, #hivm.address_space<ub>>) outs(%[[ALLOC_0]] : memref<24576xf32, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%alloc_0, %alloc_0 : memref<24576xf32, #hivm.address_space<ub>>,
                        memref<24576xf32, #hivm.address_space<ub>>)
                    outs(%alloc_1 : memref<24576xf32, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.store ins(%[[ALLOC_0]] : memref<24576xf32, #hivm.address_space<ub>>) outs({{.*}})
      hivm.hir.store ins(%alloc_1 : memref<24576xf32, #hivm.address_space<ub>>)
                     outs(%arg1 : memref<24576xf32, #hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_infer_not_inplace_loadstore_under_loop
  func.func @test_infer_not_inplace_loadstore_under_loop(%arg0 : memref<16xf16, #hivm.address_space<gm>>,
                                                         %arg1 : memref<16xf16, #hivm.address_space<gm>>,
                                                         %arg2 : memref<16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST5:.*]] = arith.constant 160 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 64 : i64
    // CHECK: %[[CONST4:.*]] = arith.constant 128 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32 : i64
    // CHECK: %[[CONST3:.*]] = arith.constant 96 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %start = arith.constant 0 : index
    %end = arith.constant 1024 : index
    %step = arith.constant 128 : index
    scf.for %iv = %start to %end step %step {
      // CHECK: %[[ALLOC_0:.*]] = hivm.hir.pointer_cast(%[[CONST2]], %[[CONST5]])
      // CHECK: %[[ALLOC_1:.*]] = hivm.hir.pointer_cast(%[[CONST1]], %[[CONST4]])
      // CHECK: %[[ALLOC_2:.*]] = hivm.hir.pointer_cast(%[[CONST0]], %[[CONST3]])
      // CHECK: %[[ALLOC_3:.*]] = hivm.hir.pointer_cast(%[[CONST0]], %[[CONST3]])
      %alloc_0 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %alloc_0 {hivm.multi_buffer = 2 : i32} : memref<16xf16, #hivm.address_space<ub>>
      %alloc_1 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      %alloc_2 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %alloc_2 {hivm.multi_buffer = 2 : i32} : memref<16xf16, #hivm.address_space<ub>>
      %alloc_3 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %alloc_3 {hivm.multi_buffer = 2 : i32} : memref<16xf16, #hivm.address_space<ub>>
      // CHECK: hivm.hir.load ins({{.*}}) outs(%[[ALLOC_2]] : memref<16xf16, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.load ins({{.*}}) outs(%[[ALLOC_1]] : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.load ins(%arg0 : memref<16xf16, #hivm.address_space<gm>>)
                     outs(%alloc_0 : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.load ins(%arg1 : memref<16xf16, #hivm.address_space<gm>>)
                    outs(%alloc_2 : memref<16xf16, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.vadd ins(%[[ALLOC_2]], %[[ALLOC_2]] : memref<16xf16, #hivm.address_space<ub>>, memref<16xf16, #hivm.address_space<ub>>) outs(%[[ALLOC_2]] : memref<16xf16, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.vadd ins(%[[ALLOC_2]], %[[ALLOC_1]] : memref<16xf16, #hivm.address_space<ub>>, memref<16xf16, #hivm.address_space<ub>>) outs(%[[ALLOC_0]] : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%alloc_0, %alloc_0 : memref<16xf16, #hivm.address_space<ub>>,
                        memref<16xf16, #hivm.address_space<ub>>)
                    outs(%alloc_1 : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%alloc_1, %alloc_2 : memref<16xf16, #hivm.address_space<ub>>,
                        memref<16xf16, #hivm.address_space<ub>>)
                    outs(%alloc_3 : memref<16xf16, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.store ins(%[[ALLOC_0]] : memref<16xf16, #hivm.address_space<ub>>) outs({{.*}})
      hivm.hir.store ins(%alloc_3 : memref<16xf16, #hivm.address_space<ub>>)
                     outs(%arg2 : memref<16xf16, #hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
    // CHECK-LABEL: func.func @vadd_inplace
    func.func @vadd_inplace(%lhs_gm : memref<16x16xf16, #hivm.address_space<gm>>,
                             %rhs_gm :  memref<16x16xf16, #hivm.address_space<gm>>,
                             %vadd_gm :memref<16x16xf16, #hivm.address_space<gm>>) {
      // CHECK-NOT: memref.alloc()
      // CHECK: %[[CONST1:.*]] = arith.constant 512 : i64
      // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
      // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
      %lhs_ub = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%lhs_gm : memref<16x16xf16, #hivm.address_space<gm>>)
                    outs(%lhs_ub : memref<16x16xf16, #hivm.address_space<ub>>)
      // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
      %rhs_ub = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%rhs_gm : memref<16x16xf16, #hivm.address_space<gm>>)
                    outs(%rhs_ub : memref<16x16xf16, #hivm.address_space<ub>>)
      // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
      %vadd_res_ub = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%lhs_ub, %rhs_ub : memref<16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16xf16, #hivm.address_space<ub>>)
                    outs(%vadd_res_ub : memref<16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.store ins(%vadd_res_ub : memref<16x16xf16, #hivm.address_space<ub>>)
                     outs(%vadd_gm : memref<16x16xf16, #hivm.address_space<gm>>)
      return
    }
}

// -----
module {
    // CHECK-LABEL: func.func @vcast_inplace_2d_small_to_large_invalid
    func.func @vcast_inplace_2d_small_to_large_invalid(%arg0_gm : memref<16x16xf16, #hivm.address_space<gm>>,
                                                       %vcast_gm :memref<16x16xf32, #hivm.address_space<gm>>) {
      // CHECK-NOT: memref.alloc()
      // CHECK: %[[CONST512:.*]] = arith.constant 512 : i64
      // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
      // CHECK: %[[SRC:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %arg0_ub = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0_gm : memref<16x16xf16, #hivm.address_space<gm>>)
                    outs(%arg0_ub : memref<16x16xf16, #hivm.address_space<ub>>)
      // CHECK: %[[DST:.*]] = hivm.hir.pointer_cast(%[[CONST512]])
      %vcast_res_ub = memref.alloc() : memref<16x16xf32, #hivm.address_space<ub>>
      // CHECK: hivm.hir.vcast ins(%[[SRC]] : memref<16x16xf16, #hivm.address_space<ub>>) outs(%[[DST]] : memref<16x16xf32, #hivm.address_space<ub>>)
      hivm.hir.vcast ins(%arg0_ub : memref<16x16xf16, #hivm.address_space<ub>>)
      outs(%vcast_res_ub : memref<16x16xf32, #hivm.address_space<ub>>) round_mode = #hivm.round_mode<rint>
      hivm.hir.store ins(%vcast_res_ub : memref<16x16xf32, #hivm.address_space<ub>>)
                     outs(%vcast_gm : memref<16x16xf32, #hivm.address_space<gm>>)
      return
    }
}

// -----
module {
    // CHECK-LABEL: func.func @vcast_inplace_2d_equal_valid
    func.func @vcast_inplace_2d_equal_valid(%arg0_gm : memref<16x16xf16, #hivm.address_space<gm>>,
                                            %vcast_gm :memref<16x16xi16, #hivm.address_space<gm>>) {
      // CHECK-NOT: memref.alloc()
      // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
      // CHECK: %[[SRC:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %arg0_ub = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0_gm : memref<16x16xf16, #hivm.address_space<gm>>)
                    outs(%arg0_ub : memref<16x16xf16, #hivm.address_space<ub>>)
      // CHECK: %[[DST:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %vcast_res_ub = memref.alloc() : memref<16x16xi16, #hivm.address_space<ub>>
      // CHECK: hivm.hir.vcast ins(%[[SRC]] : memref<16x16xf16, #hivm.address_space<ub>>) outs(%[[DST]] : memref<16x16xi16, #hivm.address_space<ub>>) round_mode = <trunc>
      hivm.hir.vcast ins(%arg0_ub : memref<16x16xf16, #hivm.address_space<ub>>)
      outs(%vcast_res_ub : memref<16x16xi16, #hivm.address_space<ub>>) round_mode = #hivm.round_mode<trunc>
      hivm.hir.store ins(%vcast_res_ub : memref<16x16xi16, #hivm.address_space<ub>>)
                     outs(%vcast_gm : memref<16x16xi16, #hivm.address_space<gm>>)
      return
    }
}

// -----
module {
    // CHECK-LABEL: func.func @vcast_inplace_2d_large_to_small_invalid
    func.func @vcast_inplace_2d_large_to_small_invalid(%arg0_gm : memref<16x16xf32, #hivm.address_space<gm>>,
                                                       %vcast_gm :memref<16x16xf16, #hivm.address_space<gm>>) {
      // CHECK-NOT: memref.alloc()
      // CHECK: %[[CONST1024:.*]] = arith.constant 1024 : i64
      // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
      // CHECK: %[[SRC:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %arg0_ub = memref.alloc() : memref<16x16xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0_gm : memref<16x16xf32, #hivm.address_space<gm>>)
                    outs(%arg0_ub : memref<16x16xf32, #hivm.address_space<ub>>)
      // CHECK: %[[DST:.*]] = hivm.hir.pointer_cast(%[[CONST1024]])
      %vcast_res_ub = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
      // CHECK: hivm.hir.vcast ins(%[[SRC]] : memref<16x16xf32, #hivm.address_space<ub>>) outs(%[[DST]] : memref<16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.vcast ins(%arg0_ub : memref<16x16xf32, #hivm.address_space<ub>>)
      outs(%vcast_res_ub : memref<16x16xf16, #hivm.address_space<ub>>) round_mode = #hivm.round_mode<trunc>
      hivm.hir.store ins(%vcast_res_ub : memref<16x16xf16, #hivm.address_space<ub>>)
                     outs(%vcast_gm : memref<16x16xf16, #hivm.address_space<gm>>)
      return
    }
}

// -----
module {
    // CHECK-LABEL: func.func @vcast_inplace_1d_small_to_large_invalid
    func.func @vcast_inplace_1d_small_to_large_invalid(%arg0_gm : memref<1024xf16, #hivm.address_space<gm>>,
                                                       %vcast_gm :memref<1024xf32, #hivm.address_space<gm>>) {
      // CHECK-NOT: memref.alloc()
      // CHECK: %[[CONST2048:.*]] = arith.constant 2048 : i64
      // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
      // CHECK: %[[SRC:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %arg0_ub = memref.alloc() : memref<1024xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0_gm : memref<1024xf16, #hivm.address_space<gm>>)
                    outs(%arg0_ub : memref<1024xf16, #hivm.address_space<ub>>)
      // CHECK: %[[DST:.*]] = hivm.hir.pointer_cast(%[[CONST2048]])
      %vcast_res_ub = memref.alloc() : memref<1024xf32, #hivm.address_space<ub>>
      // CHECK: hivm.hir.vcast ins(%[[SRC]] : memref<1024xf16, #hivm.address_space<ub>>) outs(%[[DST]] : memref<1024xf32, #hivm.address_space<ub>>)
      hivm.hir.vcast ins(%arg0_ub : memref<1024xf16, #hivm.address_space<ub>>)
      outs(%vcast_res_ub : memref<1024xf32, #hivm.address_space<ub>>) round_mode = #hivm.round_mode<rint>
      hivm.hir.store ins(%vcast_res_ub : memref<1024xf32, #hivm.address_space<ub>>)
                     outs(%vcast_gm : memref<1024xf32, #hivm.address_space<gm>>)
      return
    }
}

// -----
module {
    // CHECK-LABEL: func.func @vcast_inplace_1d_equal_valid
    func.func @vcast_inplace_1d_equal_valid(%arg0_gm : memref<1024xf16, #hivm.address_space<gm>>,
                                            %vcast_gm :memref<1024xi16, #hivm.address_space<gm>>) {
      // CHECK-NOT: memref.alloc()
      // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
      // CHECK: %[[SRC:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %arg0_ub = memref.alloc() : memref<1024xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0_gm : memref<1024xf16, #hivm.address_space<gm>>)
                    outs(%arg0_ub : memref<1024xf16, #hivm.address_space<ub>>)
      // CHECK: %[[DST:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %vcast_res_ub = memref.alloc() : memref<1024xi16, #hivm.address_space<ub>>
      // CHECK: hivm.hir.vcast ins(%[[SRC]] : memref<1024xf16, #hivm.address_space<ub>>) outs(%[[DST]] : memref<1024xi16, #hivm.address_space<ub>>) round_mode = <trunc>
      hivm.hir.vcast ins(%arg0_ub : memref<1024xf16, #hivm.address_space<ub>>)
      outs(%vcast_res_ub : memref<1024xi16, #hivm.address_space<ub>>) round_mode = #hivm.round_mode<trunc>
      hivm.hir.store ins(%vcast_res_ub : memref<1024xi16, #hivm.address_space<ub>>)
                     outs(%vcast_gm : memref<1024xi16, #hivm.address_space<gm>>)
      return
    }
}

// -----
module {
    // CHECK-LABEL: func.func @vcast_inplace_1d_large_to_small_valid
    func.func @vcast_inplace_1d_large_to_small_valid(%arg0_gm : memref<1024xf32, #hivm.address_space<gm>>,
                                                     %vcast_gm :memref<1024xf16, #hivm.address_space<gm>>) {
      // CHECK-NOT: memref.alloc()
      // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
      // CHECK: %[[SRC:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %arg0_ub = memref.alloc() : memref<1024xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0_gm : memref<1024xf32, #hivm.address_space<gm>>)
                    outs(%arg0_ub : memref<1024xf32, #hivm.address_space<ub>>)
      // CHECK: %[[DST:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %vcast_res_ub = memref.alloc() : memref<1024xf16, #hivm.address_space<ub>>
      // CHECK: hivm.hir.vcast ins(%[[SRC]] : memref<1024xf32, #hivm.address_space<ub>>) outs(%[[DST]] : memref<1024xf16, #hivm.address_space<ub>>)
      hivm.hir.vcast ins(%arg0_ub : memref<1024xf32, #hivm.address_space<ub>>)
      outs(%vcast_res_ub : memref<1024xf16, #hivm.address_space<ub>>) round_mode = #hivm.round_mode<trunc>
      hivm.hir.store ins(%vcast_res_ub : memref<1024xf16, #hivm.address_space<ub>>)
                     outs(%vcast_gm : memref<1024xf16, #hivm.address_space<gm>>)
      return
    }
}

// -----
module {
    // CHECK-LABEL: func.func @vcast_inplace_1d_s2l_stride2_invalid
    func.func @vcast_inplace_1d_s2l_stride2_invalid(%arg0_gm : memref<1024xf16, strided<[2]>, #hivm.address_space<gm>>,
                                                    %vcast_gm :memref<1024xf32, strided<[2]>, #hivm.address_space<gm>>) {
      // CHECK-NOT: memref.alloc()
      // CHECK: %[[CONST2048:.*]] = arith.constant 2048 : i64
      // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
      // CHECK: %[[SRC:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %arg0_ub = memref.alloc() : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0_gm : memref<1024xf16, strided<[2]>, #hivm.address_space<gm>>)
                    outs(%arg0_ub : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>)
      // CHECK: %[[DST:.*]] = hivm.hir.pointer_cast(%[[CONST2048]])
      %vcast_res_ub = memref.alloc() : memref<1024xf32, strided<[2]>, #hivm.address_space<ub>>
      // CHECK: hivm.hir.vcast ins(%[[SRC]] : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>) outs(%[[DST]] : memref<1024xf32, strided<[2]>, #hivm.address_space<ub>>)
      hivm.hir.vcast ins(%arg0_ub : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>)
      outs(%vcast_res_ub : memref<1024xf32, strided<[2]>, #hivm.address_space<ub>>) round_mode = #hivm.round_mode<rint>
      hivm.hir.store ins(%vcast_res_ub : memref<1024xf32, strided<[2]>, #hivm.address_space<ub>>)
                     outs(%vcast_gm : memref<1024xf32, strided<[2]>, #hivm.address_space<gm>>)
      return
    }
}

// -----
module {
    // CHECK-LABEL: func.func @vcast_inplace_1d_equal_stride2_valid
    func.func @vcast_inplace_1d_equal_stride2_valid(%arg0_gm : memref<1024xf16, strided<[2]>, #hivm.address_space<gm>>,
                                                    %vcast_gm :memref<1024xi16, strided<[2]>, #hivm.address_space<gm>>) {
      // CHECK-NOT: memref.alloc()
      // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
      // CHECK: %[[SRC:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %arg0_ub = memref.alloc() : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0_gm : memref<1024xf16, strided<[2]>, #hivm.address_space<gm>>)
                    outs(%arg0_ub : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>)
      // CHECK: %[[DST:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %vcast_res_ub = memref.alloc() : memref<1024xi16, strided<[2]>, #hivm.address_space<ub>>
      // CHECK: hivm.hir.vcast ins(%[[SRC]] : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>) outs(%[[DST]] : memref<1024xi16, strided<[2]>, #hivm.address_space<ub>>) round_mode = <trunc>
      hivm.hir.vcast ins(%arg0_ub : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>)
      outs(%vcast_res_ub : memref<1024xi16, strided<[2]>, #hivm.address_space<ub>>) round_mode = #hivm.round_mode<trunc>
      hivm.hir.store ins(%vcast_res_ub : memref<1024xi16, strided<[2]>, #hivm.address_space<ub>>)
                     outs(%vcast_gm : memref<1024xi16, strided<[2]>, #hivm.address_space<gm>>)
      return
    }
}

// -----
module {
    // CHECK-LABEL: func.func @vcast_inplace_1d_l2s_stride2_invalid
    func.func @vcast_inplace_1d_l2s_stride2_invalid(%arg0_gm : memref<1024xf32, strided<[2]>, #hivm.address_space<gm>>,
                                                    %vcast_gm :memref<1024xf16, strided<[2]>, #hivm.address_space<gm>>) {
      // CHECK-NOT: memref.alloc()
      // CHECK: %[[CONST4096:.*]] = arith.constant 4096 : i64
      // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
      // CHECK: %[[SRC:.*]] = hivm.hir.pointer_cast(%[[CONST0]])
      %arg0_ub = memref.alloc() : memref<1024xf32, strided<[2]>, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0_gm : memref<1024xf32, strided<[2]>, #hivm.address_space<gm>>)
                    outs(%arg0_ub : memref<1024xf32, strided<[2]>, #hivm.address_space<ub>>)
      // CHECK: %[[DST:.*]] = hivm.hir.pointer_cast(%[[CONST4096]])
      %vcast_res_ub = memref.alloc() : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>
      // CHECK: hivm.hir.vcast ins(%[[SRC]] : memref<1024xf32, strided<[2]>, #hivm.address_space<ub>>) outs(%[[DST]] : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>)
      hivm.hir.vcast ins(%arg0_ub : memref<1024xf32, strided<[2]>, #hivm.address_space<ub>>)
      outs(%vcast_res_ub : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>) round_mode = #hivm.round_mode<trunc>
      hivm.hir.store ins(%vcast_res_ub : memref<1024xf16, strided<[2]>, #hivm.address_space<ub>>)
                     outs(%vcast_gm : memref<1024xf16, strided<[2]>, #hivm.address_space<gm>>)
      return
    }
}

// -----

module {
  // CHECK-LABEL: func.func @test_infer_plan_memory_if_yield
  func.func @test_infer_plan_memory_if_yield(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                             %alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                             %alloc6 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                             %alloc8 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                             %cond: i1) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST4:.*]] = arith.constant 24576 : i64
    // CHECK: %[[CONST3:.*]] = arith.constant 16384 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32768 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc3 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]])
    %alloc5 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc7 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST3]])
    %alloc9 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST4]])
    %alloc10 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                 outs(%alloc1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.load ins(%alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%alloc5 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.load ins(%alloc6 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%alloc9 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.load ins(%alloc8 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%alloc10 : memref<16x16x16xf16, #hivm.address_space<ub>>)

    %0 = scf.if %cond -> (memref<16x16x16xf16, #hivm.address_space<ub>>) {
      hivm.hir.vadd ins(%alloc1, %alloc9 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16x16xf16, #hivm.address_space<ub>>)
                    outs(%alloc3 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      scf.yield %alloc3: memref<16x16x16xf16, #hivm.address_space<ub>>
    } else {
      hivm.hir.vadd ins(%alloc5, %alloc10 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16x16xf16, #hivm.address_space<ub>>)
          outs(%alloc7 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      scf.yield %alloc7 : memref<16x16x16xf16, #hivm.address_space<ub>>
    }
    hivm.hir.store ins(%0 : memref<16x16x16xf16, #hivm.address_space<ub>>)
                   outs(%alloc8 : memref<16x16x16xf16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_plan_memory_for_result
  func.func @test_plan_memory_for_result(%arg0: memref<16x16x16xf16, #hivm.address_space<gm>>,
                                         %arg1: memref<16x16x16xf16, #hivm.address_space<gm>>,
                                         %arg2: memref<16x16x16xf16, #hivm.address_space<gm>>,
                                         %arg3: memref<16x16x16xf16, #hivm.address_space<gm>>) ->
                                         memref<16x16x16xf16, #hivm.address_space<ub>> {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST2:.*]] = arith.constant 16384 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK:  hivm.hir.pointer_cast(%[[CONST0]])
    %0 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %2 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]])
    %3 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]])
    %4 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%0 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg1 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                      outs(%1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg3 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                      outs(%4 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.vadd ins(%0, %1 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                      memref<16x16x16xf16, #hivm.address_space<ub>>)
                      outs(%2 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    %c128 = arith.constant 128 : index
    %c1024 = arith.constant 1024 : index
    %c0 = arith.constant 0 : index
    %5 = scf.for %arg4 = %c0 to %c1024 step %c128 iter_args(%arg5 = %4) ->
         (memref<16x16x16xf16, #hivm.address_space<ub>>) {
      hivm.hir.vadd ins(%0, %arg5 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16x16xf16, #hivm.address_space<ub>>)
                    outs(%3 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      scf.yield %3 : memref<16x16x16xf16, #hivm.address_space<ub>>
    }
    hivm.hir.store ins(%2 : memref<16x16x16xf16, #hivm.address_space<ub>>)
                   outs(%arg2 : memref<16x16x16xf16, #hivm.address_space<gm>>)
    return %5 : memref<16x16x16xf16, #hivm.address_space<ub>>
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_plan_memory_subview
  func.func @test_plan_memory_subview(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                      %alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                      %alloc6 : memref<16x2x16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc3 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc5 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%alloc1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.load ins(%alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%alloc3 : memref<16x16x16xf16, #hivm.address_space<ub>>)

    hivm.hir.vadd ins(%alloc1, %alloc3: memref<16x16x16xf16, #hivm.address_space<ub>>,
                      memref<16x16x16xf16, #hivm.address_space<ub>>)
          outs(%alloc5: memref<16x16x16xf16, #hivm.address_space<ub>>)
    %0 = memref.subview %alloc5[0, 0, 0] [16, 2, 16] [1, 1, 1] :
         memref<16x16x16xf16, #hivm.address_space<ub>> to
         memref<16x2x16xf16, strided<[256, 16, 1]>, #hivm.address_space<ub>>

    hivm.hir.store ins(%0: memref<16x2x16xf16, strided<[256, 16, 1]>, #hivm.address_space<ub>>)
                   outs(%alloc6: memref<16x2x16xf16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_plan_memory_subview_in_loop
  func.func @test_plan_memory_subview_in_loop(%arg0 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                              %arg1 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                              %arg2 : memref<16x2x16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST5:.*]] = arith.constant 40960 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 16384 : i64
    // CHECK: %[[CONST4:.*]] = arith.constant 32768 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST3:.*]] = arith.constant 24576 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %start = arith.constant 0 : index
    %end = arith.constant 1024 : index
    %step = arith.constant 128 : index
    scf.for %iv = %start to %end step %step {
      // CHECK: hivm.hir.pointer_cast(%[[CONST2]], %[[CONST5]])
      // CHECK: hivm.hir.pointer_cast(%[[CONST1]], %[[CONST4]])
      // CHECK: hivm.hir.pointer_cast(%[[CONST0]], %[[CONST3]])
      %alloc_0 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      annotation.mark %alloc_0 {hivm.multi_buffer = 2 : i32} : memref<16x16x16xf16, #hivm.address_space<ub>>
      %alloc_1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      annotation.mark %alloc_1 {hivm.multi_buffer = 2 : i32} : memref<16x16x16xf16, #hivm.address_space<ub>>
      %alloc_2 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      annotation.mark %alloc_2 {hivm.multi_buffer = 2 : i32} : memref<16x16x16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                    outs(%alloc_0 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.load ins(%arg1 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                    outs(%alloc_1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%alloc_0, %alloc_1: memref<16x16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16x16xf16, #hivm.address_space<ub>>)
            outs(%alloc_2: memref<16x16x16xf16, #hivm.address_space<ub>>)
      %subview = memref.subview %alloc_2[0, 0, 0] [16, 2, 16] [1, 1, 1] :
          memref<16x16x16xf16, #hivm.address_space<ub>> to
          memref<16x2x16xf16, strided<[256, 16, 1]>, #hivm.address_space<ub>>

      hivm.hir.store ins(%subview: memref<16x2x16xf16, strided<[256, 16, 1]>, #hivm.address_space<ub>>)
                    outs(%arg2: memref<16x2x16xf16, #hivm.address_space<gm>>)
    }
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_plan_memory_collapse_shape
  func.func @test_plan_memory_collapse_shape(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                             %alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                             %alloc6 : memref<256x16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc3 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc5 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%alloc1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.load ins(%alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%alloc3 : memref<16x16x16xf16, #hivm.address_space<ub>>)

    hivm.hir.vadd ins(%alloc1, %alloc3: memref<16x16x16xf16, #hivm.address_space<ub>>,
                      memref<16x16x16xf16, #hivm.address_space<ub>>)
                  outs(%alloc5: memref<16x16x16xf16, #hivm.address_space<ub>>)
    %0 = memref.collapse_shape %alloc5 [[0, 1], [2]] :
         memref<16x16x16xf16, #hivm.address_space<ub>> into memref<256x16xf16, #hivm.address_space<ub>>
    hivm.hir.store ins(%0: memref<256x16xf16, #hivm.address_space<ub>>)
                   outs(%alloc6: memref<256x16xf16, #hivm.address_space<gm>>)
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_plan_memory_expand_shape
  func.func @test_plan_memory_expand_shape(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                           %alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                           %alloc6 : memref<2x8x16x16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc3 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc5 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%alloc1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.load ins(%alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%alloc3 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.vadd ins(%alloc1, %alloc3: memref<16x16x16xf16, #hivm.address_space<ub>>,
                      memref<16x16x16xf16, #hivm.address_space<ub>>)
                  outs(%alloc5: memref<16x16x16xf16, #hivm.address_space<ub>>)

    %0 = memref.expand_shape %alloc5 [[0, 1], [2], [3]] output_shape [2, 8, 16, 16] :
         memref<16x16x16xf16, #hivm.address_space<ub>> into memref<2x8x16x16xf16, #hivm.address_space<ub>>
    hivm.hir.store ins(%0: memref<2x8x16x16xf16, #hivm.address_space<ub>>)
                   outs(%alloc6: memref<2x8x16x16xf16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_plan_memory_temp_buffer
  func.func @test_plan_memory_temp_buffer(%arg0: memref<1x10xi16, #hivm.address_space<gm>>,
                                          %arg1: memref<8x10xi16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST0:.*]] = arith.constant 192 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]])
    %alloc = memref.alloc() : memref<1x10xi16, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<1x10xi16, #hivm.address_space<gm>>)
                  outs(%alloc : memref<1x10xi16, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc_0 = memref.alloc() : memref<8x10xi16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc_1 = memref.alloc() : memref<80xi16, #hivm.address_space<ub>>
    hivm.hir.vbrc ins(%alloc : memref<1x10xi16, #hivm.address_space<ub>>)
                  outs(%alloc_0 : memref<8x10xi16, #hivm.address_space<ub>>)
                  temp_buffer(%alloc_1 : memref<80xi16, #hivm.address_space<ub>>) broadcast_dims = [0]
    hivm.hir.store ins(%alloc_0 : memref<8x10xi16, #hivm.address_space<ub>>)
                   outs(%arg1 : memref<8x10xi16, #hivm.address_space<gm>>)
    return
  }
}

// -----
#map = affine_map<()[s0] -> (s0 * 1572864)>
#map1 = affine_map<(d0) -> ((d0 floordiv 2048) mod 2)>
module {
  // CHECK-LABEL: func.func @test_plan_memory_select
  func.func @test_plan_memory_select(%arg0: memref<31457280xf32, #hivm.address_space<gm>>,
                                     %arg1: memref<31457280xf32, #hivm.address_space<gm>>,
                                     %arg2: memref<31457280xf32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK-DAG: %[[CONST5:.*]] = arith.constant 8192 : i64
    // CHECK-DAG: %[[CONST4:.*]] = arith.constant 32768 : i64
    // CHECK-DAG: %[[CONST3:.*]] = arith.constant 0 : i64
    // CHECK-DAG: %[[CONST2:.*]] = arith.constant 40960 : i64
    // CHECK-DAG: %[[CONST1:.*]] = arith.constant 16384 : i64
    // CHECK-DAG: %[[CONST0:.*]] = arith.constant 24576 : i64
    %c0 = arith.constant 0 : index
    %c1572864 = arith.constant 1572864 : index
    %c2048 = arith.constant 2048 : index
    %0 = hivm.hir.get_block_idx -> i64
    %1 = arith.index_cast %0 : i64 to index
    %2 = affine.apply #map()[%1]
    %subview = memref.subview %arg0[%2] [1572864] [1] : memref<31457280xf32, #hivm.address_space<gm>> to
               memref<1572864xf32, strided<[1], offset: ?>, #hivm.address_space<gm>>
    %subview_0 = memref.subview %arg2[%2] [1572864] [1] : memref<31457280xf32, #hivm.address_space<gm>> to
                 memref<1572864xf32, strided<[1], offset: ?>, #hivm.address_space<gm>>
    %subview_1 = memref.subview %arg1[%2] [1572864] [1] : memref<31457280xf32, #hivm.address_space<gm>> to
                 memref<1572864xf32, strided<[1], offset: ?>, #hivm.address_space<gm>>
    // CHECK-NOT: memref.alloc()
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc = memref.alloc() {alignment = 64 : i64} : memref<2048xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc_2 = memref.alloc() {alignment = 64 : i64} : memref<2048xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]])
    %alloc_3 = memref.alloc() {alignment = 64 : i64} : memref<2048xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST3]])
    %alloc_4 = memref.alloc() {alignment = 64 : i64} : memref<2048xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST4]])
    %alloc_5 = memref.alloc() {alignment = 64 : i64} : memref<2048xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST5]])
    %alloc_6 = memref.alloc() {alignment = 64 : i64} : memref<2048xf32, #hivm.address_space<ub>>
    scf.for %arg3 = %c0 to %c1572864 step %c2048 {
      %3 = affine.apply #map1(%arg3)
      %4 = arith.index_cast %3 : index to i1
      %5 = arith.select %4, %alloc_5, %alloc_6 : memref<2048xf32, #hivm.address_space<ub>>
      %6 = affine.apply #map1(%arg3)
      %7 = arith.index_cast %6 : index to i1
      %8 = arith.select %7, %alloc_3, %alloc_4 : memref<2048xf32, #hivm.address_space<ub>>
      %9 = affine.apply #map1(%arg3)
      %10 = arith.index_cast %9 : index to i1
      %11 = arith.select %10, %alloc, %alloc_2 : memref<2048xf32, #hivm.address_space<ub>>
      %subview_7 = memref.subview %subview[%arg3] [2048] [1] :
                   memref<1572864xf32, strided<[1], offset: ?>, #hivm.address_space<gm>> to
                   memref<2048xf32, strided<[1], offset: ?>, #hivm.address_space<gm>>
      hivm.hir.load ins(%subview_7 : memref<2048xf32, strided<[1], offset: ?>, #hivm.address_space<gm>>)
                    outs(%11 : memref<2048xf32, #hivm.address_space<ub>>)
      %subview_8 = memref.subview %subview_1[%arg3] [2048] [1] :
                   memref<1572864xf32, strided<[1], offset: ?>, #hivm.address_space<gm>> to
                   memref<2048xf32, strided<[1], offset: ?>, #hivm.address_space<gm>>
      hivm.hir.load ins(%subview_8 :
                    memref<2048xf32, strided<[1], offset: ?>, #hivm.address_space<gm>>)
                    outs(%8 : memref<2048xf32, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%11, %8 : memref<2048xf32, #hivm.address_space<ub>>,
                    memref<2048xf32, #hivm.address_space<ub>>)
                    outs(%5 : memref<2048xf32, #hivm.address_space<ub>>)
      %subview_9 = memref.subview %subview_0[%arg3] [2048] [1] :
                   memref<1572864xf32, strided<[1], offset: ?>, #hivm.address_space<gm>> to
                   memref<2048xf32, strided<[1], offset: ?>, #hivm.address_space<gm>>
      hivm.hir.store ins(%5 : memref<2048xf32, #hivm.address_space<ub>>)
                     outs(%subview_9 : memref<2048xf32, strided<[1], offset: ?>, #hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_plan_memory_memref_view_and_reinterpret_cast
  func.func @test_plan_memory_memref_view_and_reinterpret_cast(%arg0: memref<1x?xi16, #hivm.address_space<gm>>,
                                                                 %arg1: memref<?x1xi16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c1 = arith.constant 1 : index
    %c0 = arith.constant 0 : index
    %dim = memref.dim %arg0, %c1 : memref<1x?xi16, #hivm.address_space<gm>>
    %dim_0 = memref.dim %arg1, %c1 : memref<?x1xi16, #hivm.address_space<gm>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc = memref.alloc() : memref<2048xi8, #hivm.address_space<ub>>
    %view = memref.view %alloc[%c0][%dim] : memref<2048xi8, #hivm.address_space<ub>> to
                                            memref<1x?xi16, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<1x?xi16, #hivm.address_space<gm>>)
                  outs(%view : memref<1x?xi16, #hivm.address_space<ub>>)
    %reinterpret_cast = memref.reinterpret_cast %view to offset: [0], sizes: [%dim, 1], strides: [1, 1] :
                        memref<1x?xi16, #hivm.address_space<ub>> to
                        memref<?x1xi16, #hivm.address_space<ub>>
    hivm.hir.store ins(%reinterpret_cast : memref<?x1xi16, #hivm.address_space<ub>>)
                   outs(%arg1 : memref<?x1xi16, #hivm.address_space<gm>>)
      return
  }

}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_plan_pipe_opt
  func.func @test_mem_plan_pipe_opt(%arg1 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                    %arg2 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                    %arg3 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                    %arg4 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                    %arg5 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                    %arg6 : memref<16x16x16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST2:.*]] = arith.constant 16384 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %0 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]])
    %2 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%0 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.store ins(%0 : memref<16x16x16xf16,#hivm.address_space<ub>>)
                   outs(%arg2 : memref<16x16x16xf16, #hivm.address_space<gm>>)

    hivm.hir.load ins(%arg3 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.store ins(%1 : memref<16x16x16xf16,#hivm.address_space<ub>>)
                   outs(%arg4: memref<16x16x16xf16,#hivm.address_space<gm>>)

    hivm.hir.load ins(%arg5 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                  outs(%2 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    hivm.hir.store ins(%2 : memref<16x16x16xf16,#hivm.address_space<ub>>)
                   outs(%arg6: memref<16x16x16xf16,#hivm.address_space<gm>>)
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_plan_db_two_address
  func.func @test_mem_plan_db_two_address(%src_gm: memref<16xf16, #hivm.address_space<gm>>,
                                          %dst_gm: memref<16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 32 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0 = arith.constant 0 : index
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    scf.for %i0 = %c0 to %c16 step %c4 {
      // CHECK: hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]])
      %src_ub = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %src_ub {hivm.multi_buffer = 2 : i32} : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%src_gm : memref<16xf16, #hivm.address_space<gm>>)
                    outs(%src_ub : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.store ins(%src_ub : memref<16xf16,#hivm.address_space<ub>>)
                     outs(%dst_gm: memref<16xf16,#hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_inplace_single_and_db
  func.func @test_inplace_single_and_db(%src_gm: memref<16xf16, #hivm.address_space<gm>>,
                                        %dst_gm: memref<16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 32 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0 = arith.constant 0 : index
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    scf.for %i0 = %c0 to %c16 step %c4 {
      // CHECK: hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]])
      %src_ub = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %src_ub {hivm.multi_buffer = 2 : i32} : memref<16xf16, #hivm.address_space<ub>>
      // CHECK: hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]])
      %dst_ub = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%src_gm : memref<16xf16, #hivm.address_space<gm>>)
                    outs(%src_ub : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%src_ub, %src_ub : memref<16xf16, #hivm.address_space<ub>>,
                        memref<16xf16, #hivm.address_space<ub>>)
                    outs(%dst_ub : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.store ins(%dst_ub : memref<16xf16,#hivm.address_space<ub>>)
                     outs(%dst_gm: memref<16xf16,#hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_inplace_db_and_db
  func.func @test_mem_inplace_db_and_db(%src_gm: memref<16xf16, #hivm.address_space<gm>>,
                                        %dst_gm: memref<16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST3:.*]] = arith.constant 96 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 64 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0 = arith.constant 0 : index
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    scf.for %i0 = %c0 to %c16 step %c4 {
      // CHECK: hivm.hir.pointer_cast(%[[CONST1]], %[[CONST3]])
      // CHECK: hivm.hir.pointer_cast(%[[CONST0]], %[[CONST2]])
      %src_ub = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %src_ub {hivm.multi_buffer = 2 : i32} : memref<16xf16, #hivm.address_space<ub>>
      %dst_ub = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %dst_ub {hivm.multi_buffer = 2 : i32} : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%src_gm : memref<16xf16, #hivm.address_space<gm>>)
                    outs(%src_ub : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%src_ub, %src_ub : memref<16xf16, #hivm.address_space<ub>>,
                        memref<16xf16, #hivm.address_space<ub>>)
                    outs(%dst_ub : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.store ins(%dst_ub : memref<16xf16,#hivm.address_space<ub>>)
                     outs(%dst_gm: memref<16xf16,#hivm.address_space<gm>>)
    }
    return
  }
}

// -----

module {
  func.func @test_multi_buffer_not_on_alloc(%src_gm: memref<16x1xf16, #hivm.address_space<gm>>,
                                        %dst_gm: memref<16x1xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 512 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0 = arith.constant 0 : index
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    scf.for %i0 = %c0 to %c16 step %c4 {
      // CHECK: hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]])
      %src_ub = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
      %subview = memref.subview %src_ub[0, 0] [16, 1] [1, 1] :
         memref<16x16xf16, #hivm.address_space<ub>> to
         memref<16x1xf16, strided<[16, 1]>, #hivm.address_space<ub>>
      annotation.mark %subview {hivm.multi_buffer = 2 : i32} : memref<16x1xf16, strided<[16, 1]>, #hivm.address_space<ub>>
      hivm.hir.load ins(%src_gm : memref<16x1xf16, #hivm.address_space<gm>>)
                    outs(%subview : memref<16x1xf16, strided<[16, 1]>, #hivm.address_space<ub>>)
      hivm.hir.store ins(%subview : memref<16x1xf16, strided<[16, 1]>, #hivm.address_space<ub>>)
                     outs(%dst_gm: memref<16x1xf16,#hivm.address_space<gm>>)
    }
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_mem_plan_multi_address
  func.func @test_mem_plan_multi_address(%src_gm: memref<16xf16, #hivm.address_space<gm>>,
                                         %dst_gm: memref<16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST3:.*]] = arith.constant 96 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 64 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0 = arith.constant 0 : index
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    scf.for %i0 = %c0 to %c16 step %c4 {
      // CHECK: hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]], %[[CONST2]], %[[CONST3]])
      %src_ub = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %src_ub {hivm.multi_buffer = 4 : i32} : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%src_gm : memref<16xf16, #hivm.address_space<gm>>)
                    outs(%src_ub : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.store ins(%src_ub : memref<16xf16,#hivm.address_space<ub>>)
                     outs(%dst_gm: memref<16xf16,#hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_inplace_single_and_mb
  func.func @test_inplace_single_and_mb(%src_gm: memref<16xf16, #hivm.address_space<gm>>,
                                        %dst_gm: memref<16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST3:.*]] = arith.constant 96 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 64 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0 = arith.constant 0 : index
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    scf.for %i0 = %c0 to %c16 step %c4 {
      // CHECK: hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]], %[[CONST2]], %[[CONST3]])
      %src_ub = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %src_ub {hivm.multi_buffer = 4 : i32} : memref<16xf16, #hivm.address_space<ub>>
      // CHECK: hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]], %[[CONST2]], %[[CONST3]])
      %dst_ub = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%src_gm : memref<16xf16, #hivm.address_space<gm>>)
                    outs(%src_ub : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%src_ub, %src_ub : memref<16xf16, #hivm.address_space<ub>>,
                        memref<16xf16, #hivm.address_space<ub>>)
                    outs(%dst_ub : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.store ins(%dst_ub : memref<16xf16,#hivm.address_space<ub>>)
                     outs(%dst_gm: memref<16xf16,#hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_inplace_mb_and_mb
  func.func @test_mem_inplace_mb_and_mb(%src_gm: memref<16xf16, #hivm.address_space<gm>>,
                                        %dst_gm: memref<16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST7:.*]] = arith.constant 224 : i64
    // CHECK: %[[CONST6:.*]] = arith.constant 192 : i64
    // CHECK: %[[CONST5:.*]] = arith.constant 160 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32 : i64
    // CHECK: %[[CONST4:.*]] = arith.constant 128 : i64
    // CHECK: %[[CONST3:.*]] = arith.constant 96 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 64 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0 = arith.constant 0 : index
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    scf.for %i0 = %c0 to %c16 step %c4 {
      // CHECK: hivm.hir.pointer_cast(%[[CONST1]], %[[CONST5]], %[[CONST6]], %[[CONST7]])
      // CHECK: hivm.hir.pointer_cast(%[[CONST0]], %[[CONST2]], %[[CONST3]], %[[CONST4]])
      %src_ub = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %src_ub {hivm.multi_buffer = 4 : i32} : memref<16xf16, #hivm.address_space<ub>>
      %dst_ub = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      annotation.mark %dst_ub {hivm.multi_buffer = 4 : i32} : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%src_gm : memref<16xf16, #hivm.address_space<gm>>)
                    outs(%src_ub : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%src_ub, %src_ub : memref<16xf16, #hivm.address_space<ub>>,
                        memref<16xf16, #hivm.address_space<ub>>)
                    outs(%dst_ub : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.store ins(%dst_ub : memref<16xf16,#hivm.address_space<ub>>)
                     outs(%dst_gm: memref<16xf16,#hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_level1_single_buffer_reuse
  func.func @test_mem_level1_single_buffer_reuse(%src1_gm: memref<6144xf32, #hivm.address_space<gm>>,
                                                 %src2_gm: memref<6144xf32, #hivm.address_space<gm>>,
                                                 %dst1_gm: memref<6144xf32, #hivm.address_space<gm>>,
                                                 %dst2_gm: memref<6144xf32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    %c0 = arith.constant 0 : index
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    scf.for %i0 = %c0 to %c16 step %c4 {
      %src1_ub = memref.alloc() : memref<6144xf32, #hivm.address_space<ub>>
      annotation.mark %src1_ub {hivm.multi_buffer = 4 : i32} : memref<6144xf32, #hivm.address_space<ub>>
      %src2_ub = memref.alloc() : memref<6144xf32, #hivm.address_space<ub>>
      %dst1_ub = memref.alloc() : memref<6144xf32, #hivm.address_space<ub>>
      annotation.mark %dst1_ub {hivm.multi_buffer = 4 : i32} : memref<6144xf32, #hivm.address_space<ub>>
      %dst2_ub = memref.alloc() : memref<6144xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%src1_gm : memref<6144xf32, #hivm.address_space<gm>>)
                    outs(%src1_ub : memref<6144xf32, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%src1_ub, %src1_ub : memref<6144xf32, #hivm.address_space<ub>>,  memref<6144xf32, #hivm.address_space<ub>>)
                    outs(%dst1_ub : memref<6144xf32, #hivm.address_space<ub>>)
      hivm.hir.store ins(%dst1_ub : memref<6144xf32,#hivm.address_space<ub>>)
                     outs(%dst1_gm: memref<6144xf32,#hivm.address_space<gm>>)
      hivm.hir.load ins(%src2_gm : memref<6144xf32, #hivm.address_space<gm>>)
                    outs(%src2_ub : memref<6144xf32, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%src2_ub, %src2_ub : memref<6144xf32, #hivm.address_space<ub>>,  memref<6144xf32, #hivm.address_space<ub>>)
                    outs(%dst2_ub : memref<6144xf32, #hivm.address_space<ub>>)
      hivm.hir.store ins(%dst2_ub : memref<6144xf32,#hivm.address_space<ub>>)
                     outs(%dst2_gm: memref<6144xf32,#hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_level0_equal_multi_buffers_reuse
  func.func @test_mem_level0_equal_multi_buffers_reuse(%src1_gm: memref<5120xf32, #hivm.address_space<gm>>,
                                                       %src2_gm: memref<5120xf32, #hivm.address_space<gm>>,
                                                       %dst1_gm: memref<5120xf32, #hivm.address_space<gm>>,
                                                       %dst2_gm: memref<5120xf32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    %c0 = arith.constant 0 : index
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    scf.for %i0 = %c0 to %c16 step %c4 {
      %src1_ub = memref.alloc() : memref<5120xf32, #hivm.address_space<ub>>
      annotation.mark %src1_ub {hivm.multi_buffer = 4 : i32} : memref<5120xf32, #hivm.address_space<ub>>
      %src2_ub = memref.alloc() : memref<5120xf32, #hivm.address_space<ub>>
      annotation.mark %src2_ub {hivm.multi_buffer = 4 : i32} : memref<5120xf32, #hivm.address_space<ub>>
      %dst1_ub = memref.alloc() : memref<5120xf32, #hivm.address_space<ub>>
      annotation.mark %dst1_ub {hivm.multi_buffer = 4 : i32} : memref<5120xf32, #hivm.address_space<ub>>
      %dst2_ub = memref.alloc() : memref<5120xf32, #hivm.address_space<ub>>
      annotation.mark %dst2_ub {hivm.multi_buffer = 4 : i32} : memref<5120xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%src1_gm : memref<5120xf32, #hivm.address_space<gm>>)
                    outs(%src1_ub : memref<5120xf32, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%src1_ub, %src1_ub : memref<5120xf32, #hivm.address_space<ub>>,  memref<5120xf32, #hivm.address_space<ub>>)
                    outs(%dst1_ub : memref<5120xf32, #hivm.address_space<ub>>)
      hivm.hir.store ins(%dst1_ub : memref<5120xf32,#hivm.address_space<ub>>)
                     outs(%dst1_gm: memref<5120xf32,#hivm.address_space<gm>>)
      hivm.hir.load ins(%src2_gm : memref<5120xf32, #hivm.address_space<gm>>)
                    outs(%src2_ub : memref<5120xf32, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%src2_ub, %src2_ub : memref<5120xf32, #hivm.address_space<ub>>,  memref<5120xf32, #hivm.address_space<ub>>)
                    outs(%dst2_ub : memref<5120xf32, #hivm.address_space<ub>>)
      hivm.hir.store ins(%dst2_ub : memref<5120xf32,#hivm.address_space<ub>>)
                     outs(%dst2_gm: memref<5120xf32,#hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_level0_less_multi_buffers_reuse
  func.func @test_mem_level0_less_multi_buffers_reuse(%src1_gm: memref<5120xf32, #hivm.address_space<gm>>,
                                                      %src2_gm: memref<5120xf32, #hivm.address_space<gm>>,
                                                      %dst1_gm: memref<5120xf32, #hivm.address_space<gm>>,
                                                      %dst2_gm: memref<5120xf32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    %c0 = arith.constant 0 : index
    %c4 = arith.constant 4 : index
    %c16 = arith.constant 16 : index
    scf.for %i0 = %c0 to %c16 step %c4 {
      %src1_ub = memref.alloc() : memref<5120xf32, #hivm.address_space<ub>>
      annotation.mark %src1_ub {hivm.multi_buffer = 4 : i32} : memref<5120xf32, #hivm.address_space<ub>>
      %src2_ub = memref.alloc() : memref<5120xf32, #hivm.address_space<ub>>
      annotation.mark %src2_ub {hivm.multi_buffer = 2 : i32} : memref<5120xf32, #hivm.address_space<ub>>
      %dst1_ub = memref.alloc() : memref<5120xf32, #hivm.address_space<ub>>
      annotation.mark %dst1_ub {hivm.multi_buffer = 4 : i32} : memref<5120xf32, #hivm.address_space<ub>>
      %dst2_ub = memref.alloc() : memref<5120xf32, #hivm.address_space<ub>>
      annotation.mark %dst2_ub {hivm.multi_buffer = 2 : i32} : memref<5120xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%src1_gm : memref<5120xf32, #hivm.address_space<gm>>)
                    outs(%src1_ub : memref<5120xf32, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%src1_ub, %src1_ub : memref<5120xf32, #hivm.address_space<ub>>,  memref<5120xf32, #hivm.address_space<ub>>)
                    outs(%dst1_ub : memref<5120xf32, #hivm.address_space<ub>>)
      hivm.hir.store ins(%dst1_ub : memref<5120xf32,#hivm.address_space<ub>>)
                     outs(%dst1_gm: memref<5120xf32,#hivm.address_space<gm>>)
      hivm.hir.load ins(%src2_gm : memref<5120xf32, #hivm.address_space<gm>>)
                    outs(%src2_ub : memref<5120xf32, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%src2_ub, %src2_ub : memref<5120xf32, #hivm.address_space<ub>>,  memref<5120xf32, #hivm.address_space<ub>>)
                    outs(%dst2_ub : memref<5120xf32, #hivm.address_space<ub>>)
      hivm.hir.store ins(%dst2_ub : memref<5120xf32,#hivm.address_space<ub>>)
                     outs(%dst2_gm: memref<5120xf32,#hivm.address_space<gm>>)
    }
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_infer_mem_allocate_loop_conflict
  func.func @test_infer_mem_allocate_loop_conflict(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>,
                                                   %alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %start = arith.constant 0 : index
    %end = arith.constant 1024 : index
    %step = arith.constant 128 : index
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc3 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
    scf.for %iv = %start to %end step %step {
      // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
      %alloc1 = memref.alloc() : memref<16x16x16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%alloc2 : memref<16x16x16xf16, #hivm.address_space<gm>>)
                     outs(%alloc1 : memref<16x16x16xf16, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%alloc1, %alloc1 : memref<16x16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16x16xf16, #hivm.address_space<ub>>)
                    outs(%alloc3 : memref<16x16x16xf16, #hivm.address_space<ub>>)
    }
    hivm.hir.store ins(%alloc3 : memref<16x16x16xf16, #hivm.address_space<ub>>)
                   outs(%alloc4 : memref<16x16x16xf16, #hivm.address_space<gm>>)
    return
  }
}

// -----
module {
memref.global @__constant_3xi64 : memref<3xi64>

// CHECK-LABEL: func.func @test_plan_memory_memref_reshape
func.func @test_plan_memory_memref_reshape(%arg0: memref<2x8xi16, #hivm.address_space<gm>>,
                                           %arg1: memref<2x2x4xi16, #hivm.address_space<gm>>) {
  %0 = memref.get_global @__constant_3xi64 : memref<3xi64>
  // CHECK-NOT: memref.alloc()
  // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
  // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
  %alloc = memref.alloc() : memref<2x8xi16, #hivm.address_space<ub>>
  hivm.hir.load ins(%arg0 : memref<2x8xi16, #hivm.address_space<gm>>)
                outs(%alloc : memref<2x8xi16, #hivm.address_space<ub>>)
  %reshape = memref.reshape %alloc(%0) : (memref<2x8xi16, #hivm.address_space<ub>>, memref<3xi64>) ->
                                         memref<2x2x4xi16, #hivm.address_space<ub>>
  hivm.hir.store ins(%reshape : memref<2x2x4xi16, #hivm.address_space<ub>>)
                 outs(%arg1 : memref<2x2x4xi16, #hivm.address_space<gm>>)
    return
}
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_memref_load_store
  func.func @test_mem_memref_load_store(%alloc2 : memref<1024xf16, #hivm.address_space<gm>>,
                                        %alloc4 : memref<1024xf16, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 2048 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %start = arith.constant 0 : index
    %end = arith.constant 1024 : index
    %step = arith.constant 1 : index
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc1 = memref.alloc() : memref<1024xf16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc3 = memref.alloc() : memref<1024xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%alloc2 : memref<1024xf16, #hivm.address_space<gm>>)
                      outs(%alloc1 : memref<1024xf16, #hivm.address_space<ub>>)
    scf.for %iv = %start to %end step %step {
      %0 = memref.load %alloc1[%iv] : memref<1024xf16, #hivm.address_space<ub>>
      %1 = memref.load %alloc1[%iv] : memref<1024xf16, #hivm.address_space<ub>>
      %2 = arith.addf %0, %1 : f16
      memref.store %2, %alloc3[%iv] : memref<1024xf16, #hivm.address_space<ub>>
    }
    hivm.hir.store ins(%alloc3 : memref<1024xf16, #hivm.address_space<ub>>)
                   outs(%alloc4 : memref<1024xf16, #hivm.address_space<gm>>)
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_enough
  func.func @test_mem_enough(%arg1 : memref<1xf32, #hivm.address_space<gm>>,
                             %arg2 : memref<1xf32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST128:.*]] = arith.constant 128 : i64
    // CHECK: %[[CONST96:.*]] = arith.constant 96 : i64
    // CHECK: %[[CONST64:.*]] = arith.constant 64 : i64
    // CHECK: %[[CONST32:.*]] = arith.constant 32 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0 = arith.constant 0 : index
    %cst_0 = arith.constant 0.000000e+00 : f32
    %cst = arith.constant 1.000000e+00 : f32
    %cst_1 = arith.constant 2.44140625E-4 : f32
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %1 = memref.alloc() : memref<1xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<1xf32, #hivm.address_space<gm>>)
                  outs(%1 : memref<1xf32, #hivm.address_space<ub>>)
    %4 = memref.load %1[%c0] : memref<1xf32, #hivm.address_space<ub>>
    %5 = arith.mulf %4, %cst_1 : f32
    // CHECK: hivm.hir.pointer_cast(%[[CONST32]])
    %alloc_9 = memref.alloc() {alignment = 64 : i64} : memref<1xf32, #hivm.address_space<ub>>
    memref.store %5, %alloc_9[%c0] : memref<1xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST64]])
    %alloc_10 = memref.alloc() {alignment = 64 : i64} : memref<1xf32, #hivm.address_space<ub>>
    %6 = memref.load %alloc_9[%c0] : memref<1xf32, #hivm.address_space<ub>>
    %7 = arith.addf %6, %cst_0 : f32
    memref.store %7, %alloc_10[%c0] : memref<1xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST96]])
    %alloc_11 = memref.alloc() {alignment = 64 : i64} : memref<1xf32, #hivm.address_space<ub>>
    %8 = memref.load %alloc_10[%c0] : memref<1xf32, #hivm.address_space<ub>>
    %9 = math.sqrt %8 : f32
    memref.store %9, %alloc_11[%c0] : memref<1xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST128]])
    %alloc_12 = memref.alloc() {alignment = 64 : i64} : memref<1xf32, #hivm.address_space<ub>>
    memref.store %cst, %alloc_12[%c0] : memref<1xf32, #hivm.address_space<ub>>
    %10 = memref.load %alloc_12[%c0] : memref<1xf32, #hivm.address_space<ub>>
    %11 = memref.load %alloc_11[%c0] : memref<1xf32, #hivm.address_space<ub>>
    %12 = arith.divf %10, %11 : f32
    memref.store %12, %alloc_12[%c0] : memref<1xf32, #hivm.address_space<ub>>
    hivm.hir.store ins(%alloc_12 : memref<1xf32, #hivm.address_space<ub>>)
                   outs(%arg2 : memref<1xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_mem_for_cube
  func.func @test_mem_for_cube(%arg1 : memref<16xf32, #hivm.address_space<gm>>,
                               %arg2 : memref<16xf32, #hivm.address_space<gm>>,
                               %arg3 : memref<256xf32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 64 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %true = arith.constant true
    %c16 = arith.constant 16 : index
    %c256 = arith.constant 256 : index
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc_1 = memref.alloc() : memref<16xf32, #hivm.address_space<cbuf>>
    hivm.hir.nd2nz {dst_continuous} ins(%arg1 : memref<16xf32, #hivm.address_space<gm>>)
                    outs(%alloc_1 : memref<16xf32, #hivm.address_space<cbuf>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
    %alloc_2 = memref.alloc() : memref<16xf32, #hivm.address_space<cbuf>>
    hivm.hir.nd2nz {dst_continuous} ins(%arg1 : memref<16xf32, #hivm.address_space<gm>>)
                    outs(%alloc_2 : memref<16xf32, #hivm.address_space<cbuf>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]])
    %alloc3 = memref.alloc() : memref<256xf32, #hivm.address_space<cc>>
    hivm.hir.mmadL1 ins(%alloc_1, %alloc_2, %true, %c16, %c256, %c16 : memref<16xf32, #hivm.address_space<cbuf>>,
                        memref<16xf32, #hivm.address_space<cbuf>>, i1, index, index, index)
                        outs(%alloc3 : memref<256xf32, #hivm.address_space<cc>>)
    hivm.hir.fixpipe {enable_nz2nd} ins(%alloc3 : memref<256xf32, #hivm.address_space<cc>>)
                      outs(%arg3 : memref<256xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----
// expected-error@+1 {{ub overflow, requires 2560000 bits while 1572864 bits available! (possible reason: tiling basic block is too large or block number is more than what user expect due to multi-buffer feature is enabled and some ops need extra local buffer.)}}
func.func @test_one_mem_not_enough(%arg0_gm : memref<80000xf32, #hivm.address_space<gm>>,
                                   %vcast_gm :memref<80000xf16, #hivm.address_space<gm>>) {
  %arg0_ub = memref.alloc() : memref<80000xf32, #hivm.address_space<ub>>
  hivm.hir.load ins(%arg0_gm : memref<80000xf32, #hivm.address_space<gm>>)
                outs(%arg0_ub : memref<80000xf32, #hivm.address_space<ub>>)
  %vcast_res_ub = memref.alloc() : memref<80000xf16, #hivm.address_space<ub>>
  hivm.hir.vcast ins(%arg0_ub : memref<80000xf32, #hivm.address_space<ub>>)
  outs(%vcast_res_ub : memref<80000xf16, #hivm.address_space<ub>>) round_mode = #hivm.round_mode<trunc>
  hivm.hir.store ins(%vcast_res_ub : memref<80000xf16, #hivm.address_space<ub>>)
                  outs(%vcast_gm : memref<80000xf16, #hivm.address_space<gm>>)
  return
}

// -----
module {
  // expected-error@below {{ub overflow, requires 2379264 bits while 1572864 bits available! (possible reason: tiling basic block is too large or block number is more than what user expect due to multi-buffer feature is enabled and some ops need extra local buffer.)}}
  func.func @test_two_mem_not_enough(%arg1 : memref<37172xf32, #hivm.address_space<gm>>,
                                     %arg2 : memref<37172xf32, #hivm.address_space<gm>>) {
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 4 : index
    %1 = memref.alloc() : memref<37172xf32, #hivm.address_space<ub>>
    %2 = memref.alloc() : memref<37172xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<37172xf32, #hivm.address_space<gm>>) outs(%1 : memref<37172xf32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<37172xf32, #hivm.address_space<gm>>) outs(%2 : memref<37172xf32, #hivm.address_space<ub>>)
    %3 = memref.load %1[%c0] : memref<37172xf32, #hivm.address_space<ub>>
    %4 = memref.load %2[%c1] : memref<37172xf32, #hivm.address_space<ub>>
    %5 = arith.mulf %3, %4 : f32
    %alloc_9 = memref.alloc() {alignment = 64 : i64} : memref<37172xf32, #hivm.address_space<ub>>
    memref.store %5, %alloc_9[%c0] : memref<37172xf32, #hivm.address_space<ub>>
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_if_else
  func.func @test_if_else(%arg1 : memref<11520xi32, #hivm.address_space<gm>>,
                          %arg2 : memref<11520xi32, #hivm.address_space<gm>>,
                          %arg3 : memref<11520xf32, #hivm.address_space<gm>>,
                          %arg4: i8 {tt.divisibility = 16 : i32},
                          %arg5 : memref<11520xi64, #hivm.address_space<gm>>,
                          %arg6 : memref<11520xi64, #hivm.address_space<gm>>) {
    %cst_1 = arith.constant 4.6566126E-10 : f32
    %1 = arith.trunci %arg4 : i8 to i1
    // CHECK: %[[ARG1:.*]] = hivm.hir.pointer_cast(%[[CONST0:.*]]) : memref<11520xi32, #hivm.address_space<ub>>
    // CHECK: %[[ARG2:.*]] = hivm.hir.pointer_cast(%[[CONST1:.*]]) : memref<11520xi32, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<11520xi32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<11520xi32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<11520xi32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<11520xi32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<11520xi32, #hivm.address_space<gm>>) outs(%alloc_1 : memref<11520xi32, #hivm.address_space<ub>>)
    // CHECK: %[[ARG3:.*]] = hivm.hir.pointer_cast(%[[CONST0]]) : memref<11520xi32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<11520xi32, #hivm.address_space<ub>>
    hivm.hir.vand ins(%alloc_0, %alloc_1 : memref<11520xi32, #hivm.address_space<ub>>, memref<11520xi32, #hivm.address_space<ub>>) outs(%alloc_2 : memref<11520xi32, #hivm.address_space<ub>>)
    // CHECK: scf.if %[[ARG0:.*]] {
    scf.if %1 {
      // CHECK: %[[ARG4:.*]] = hivm.hir.pointer_cast(%[[CONST2:.*]]) : memref<11520xi64, #hivm.address_space<ub>>
      %alloc_3 = memref.alloc() : memref<11520xi64, #hivm.address_space<ub>>
      hivm.hir.vcast ins(%alloc_2 : memref<11520xi32, #hivm.address_space<ub>>) outs(%alloc_3 : memref<11520xi64, #hivm.address_space<ub>>)
      hivm.hir.store ins(%alloc_3 : memref<11520xi64, #hivm.address_space<ub>>) outs(%arg5 : memref<11520xi64, #hivm.address_space<gm>>)
    // CHECK: } else {
    } else {
      // CHECK: %[[ARG4:.*]] = hivm.hir.pointer_cast(%[[CONST1]]) : memref<11520xf32, #hivm.address_space<ub>>
      // CHECK: %[[ARG5:.*]] = hivm.hir.pointer_cast(%[[CONST3:.*]]) : memref<11520xf32, #hivm.address_space<ub>>
      // CHECK: %[[ARG6:.*]] = hivm.hir.pointer_cast(%[[CONST1]]) : memref<11520xi64, #hivm.address_space<ub>>
      %alloc_4 = memref.alloc() : memref<11520xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg3 : memref<11520xf32, #hivm.address_space<gm>>) outs(%alloc_4 : memref<11520xf32, #hivm.address_space<ub>>)
      %alloc_5 = memref.alloc() : memref<11520xf32, #hivm.address_space<ub>>
      hivm.hir.vmul ins(%alloc_4, %cst_1 : memref<11520xf32, #hivm.address_space<ub>>, f32) outs(%alloc_5 : memref<11520xf32, #hivm.address_space<ub>>)
      %alloc_6 = memref.alloc() : memref<11520xi64, #hivm.address_space<ub>>
      hivm.hir.vcast ins(%alloc_2 : memref<11520xi32, #hivm.address_space<ub>>) outs(%alloc_6 : memref<11520xi64, #hivm.address_space<ub>>)
      hivm.hir.store ins(%alloc_6 : memref<11520xi64, #hivm.address_space<ub>>) outs(%arg6 : memref<11520xi64, #hivm.address_space<gm>>)
    }
    return
  }
}

// -----

module {
  // CHECK-NOT-LABEL: func.func @test_ub_memory_without_load
  func.func @test_ub_memory_without_load(%arg0: memref<5x1xf32, #hivm.address_space<gm>>) {
    // expected-error@+1 {{'memref.alloc' op error: read before first write}}
    %alloc = memref.alloc() : memref<5x2xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<5x1xf32, #hivm.address_space<ub>>
    hivm.hir.vdeinterleave ins(%alloc : memref<5x2xf32, #hivm.address_space<ub>>) outs(%alloc_1 : memref<5x1xf32, #hivm.address_space<ub>>) channel_num = 2 index_mode = <CHANNEL_0>
    hivm.hir.store ins(%alloc_1 : memref<5x1xf32, #hivm.address_space<ub>>) outs(%arg0 : memref<5x1xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vsub_inline_broadcast_inplace
  func.func @test_vsub_inline_broadcast_inplace(%arg1 : memref<64x128xf32, #hivm.address_space<gm>>,
                                                %arg2 : memref<64x1xf32, #hivm.address_space<gm>>,
                                                %arg3 : memref<64x128xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 33024 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32768 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<64x1xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<64x1xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<64x128xf32, #hivm.address_space<gm>>) outs(%alloc : memref<64x128xf32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<64x1xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<64x1xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<512xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<512xf32, #hivm.address_space<ub>>
    hivm.hir.vsub ins(%alloc, %alloc_0 : memref<64x128xf32, #hivm.address_space<ub>>, memref<64x1xf32, #hivm.address_space<ub>>)
      outs(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<512xf32, #hivm.address_space<ub>>) broadcast = [1]
    hivm.hir.store ins(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) outs(%arg3 : memref<64x128xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vsub_inline_brc_inplace_second_src
  func.func @test_vsub_inline_brc_inplace_second_src(%arg1 : memref<64x1xf32, #hivm.address_space<gm>>,
                                                     %arg2 : memref<64x128xf32, #hivm.address_space<gm>>,
                                                     %arg3 : memref<64x128xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 33024 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 256 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x1xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<64x1xf32, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<64x1xf32, #hivm.address_space<gm>>) outs(%alloc : memref<64x1xf32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<64x128xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<64x128xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<512xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<512xf32, #hivm.address_space<ub>>
    hivm.hir.vsub ins(%alloc, %alloc_0 : memref<64x1xf32, #hivm.address_space<ub>>, memref<64x128xf32, #hivm.address_space<ub>>)
      outs(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<512xf32, #hivm.address_space<ub>>) broadcast = [1]
    hivm.hir.store ins(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) outs(%arg3 : memref<64x128xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vsub_inline_brc_inplace_dim0
  func.func @test_vsub_inline_brc_inplace_dim0(%arg1 : memref<64x128xf32, #hivm.address_space<gm>>,
                                               %arg2 : memref<1x128xf32, #hivm.address_space<gm>>,
                                               %arg3 : memref<64x128xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 33280 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32768 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<1x128xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<1x128xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<64x128xf32, #hivm.address_space<gm>>) outs(%alloc : memref<64x128xf32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<1x128xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<1x128xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<512xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<512xf32, #hivm.address_space<ub>>
    hivm.hir.vsub ins(%alloc, %alloc_0 : memref<64x128xf32, #hivm.address_space<ub>>, memref<1x128xf32, #hivm.address_space<ub>>)
      outs(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<512xf32, #hivm.address_space<ub>>) broadcast = [0]
    hivm.hir.store ins(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) outs(%arg3 : memref<64x128xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vadd_inline_broadcast_inplace
  func.func @test_vadd_inline_broadcast_inplace(%arg1 : memref<128xf32, #hivm.address_space<gm>>,
                                                %arg2 : memref<1xf32, #hivm.address_space<gm>>,
                                                %arg3 : memref<128xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 544 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 512 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<1xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<128xf32, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<1xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<128xf32, #hivm.address_space<gm>>) outs(%alloc : memref<128xf32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<1xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<1xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<128xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<128xf32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<128xf32, #hivm.address_space<ub>>
    hivm.hir.vadd ins(%alloc, %alloc_0 : memref<128xf32, #hivm.address_space<ub>>, memref<1xf32, #hivm.address_space<ub>>)
      outs(%alloc_1 : memref<128xf32, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<128xf32, #hivm.address_space<ub>>) broadcast = [0]
    hivm.hir.store ins(%alloc_1 : memref<128xf32, #hivm.address_space<ub>>) outs(%arg3 : memref<128xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vmul_inline_broadcast_inplace
  func.func @test_vmul_inline_broadcast_inplace(%arg1 : memref<4x64x128xf32, #hivm.address_space<gm>>,
                                                %arg2 : memref<4x1x128xf32, #hivm.address_space<gm>>,
                                                %arg3 : memref<4x64x128xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 133120 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 131072 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<4x64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<4x1x128xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<4x64x128xf32, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<4x1x128xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<4x64x128xf32, #hivm.address_space<gm>>) outs(%alloc : memref<4x64x128xf32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<4x1x128xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<4x1x128xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<4x64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<4096xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<4x64x128xf32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<4096xf32, #hivm.address_space<ub>>
    hivm.hir.vmul ins(%alloc, %alloc_0 : memref<4x64x128xf32, #hivm.address_space<ub>>, memref<4x1x128xf32, #hivm.address_space<ub>>)
      outs(%alloc_1 : memref<4x64x128xf32, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<4096xf32, #hivm.address_space<ub>>) broadcast = [1]
    hivm.hir.store ins(%alloc_1 : memref<4x64x128xf32, #hivm.address_space<ub>>) outs(%arg3 : memref<4x64x128xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vmax_inline_broadcast_inplace
  func.func @test_vmax_inline_broadcast_inplace(%arg1 : memref<64x128xf32, #hivm.address_space<gm>>,
                                                %arg2 : memref<64x1xf32, #hivm.address_space<gm>>,
                                                %arg3 : memref<64x128xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 33024 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32768 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<64x1xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<64x1xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<64x128xf32, #hivm.address_space<gm>>) outs(%alloc : memref<64x128xf32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<64x1xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<64x1xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<512xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<512xf32, #hivm.address_space<ub>>
    hivm.hir.vmax ins(%alloc, %alloc_0 : memref<64x128xf32, #hivm.address_space<ub>>, memref<64x1xf32, #hivm.address_space<ub>>)
      outs(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<512xf32, #hivm.address_space<ub>>) broadcast = [1]
    hivm.hir.store ins(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) outs(%arg3 : memref<64x128xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vdiv_inline_brc_inplace
  func.func @test_vdiv_inline_brc_inplace(%arg1 : memref<64x128xf32, #hivm.address_space<gm>>,
                                          %arg2 : memref<64x1xf32, #hivm.address_space<gm>>,
                                          %arg3 : memref<64x128xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 33024 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 32768 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<64x1xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<64x1xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<64x128xf32, #hivm.address_space<gm>>) outs(%alloc : memref<64x128xf32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<64x1xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<64x1xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<512xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<512xf32, #hivm.address_space<ub>>
    hivm.hir.vdiv ins(%alloc, %alloc_0 : memref<64x128xf32, #hivm.address_space<ub>>, memref<64x1xf32, #hivm.address_space<ub>>)
      outs(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<512xf32, #hivm.address_space<ub>>) broadcast = [1]
    hivm.hir.store ins(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) outs(%arg3 : memref<64x128xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vdiv_tempbuffer_vs_inplace
  func.func @test_vdiv_tempbuffer_vs_inplace(%arg1 : memref<64x128xf32, #hivm.address_space<gm>>,
                                             %arg2 : memref<64x128xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST1:.*]] = arith.constant 32768 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: %[[CST:.*]] = arith.constant 0.72134751 : f32
    %cst = arith.constant 0.72134751 : f32
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<64x128xf32, #hivm.address_space<gm>>) outs(%alloc : memref<64x128xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<64x128xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<16xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<64x128xf32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
    hivm.hir.vdiv ins(%alloc, %cst : memref<64x128xf32, #hivm.address_space<ub>>, f32)
      outs(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<16xf32, #hivm.address_space<ub>>)
    hivm.hir.store ins(%alloc_1 : memref<64x128xf32, #hivm.address_space<ub>>) outs(%arg2 : memref<64x128xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vdiv_tempbuffer_samesize_inplace_diff
  func.func @test_vdiv_tempbuffer_samesize_inplace_diff(%arg1 : memref<16xf32, #hivm.address_space<gm>>,
                                                        %arg2 : memref<16xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST1:.*]] = arith.constant 64 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: %[[CST:.*]] = arith.constant 0.72134751 : f32
    %cst = arith.constant 0.72134751 : f32
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<16xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<16xf32, #hivm.address_space<gm>>) outs(%alloc : memref<16xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<16xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<16xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
    hivm.hir.vdiv ins(%alloc, %cst : memref<16xf32, #hivm.address_space<ub>>, f32)
      outs(%alloc_1 : memref<16xf32, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<16xf32, #hivm.address_space<ub>>)
    hivm.hir.store ins(%alloc_1 : memref<16xf32, #hivm.address_space<ub>>) outs(%arg2 : memref<16xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vmul_inline_brc_bigger_not_inplace
  func.func @test_vmul_inline_brc_bigger_not_inplace(%arg1 : memref<4x1x64xf32, #hivm.address_space<gm>>,
                                                     %arg2 : memref<4x1x64xf32, #hivm.address_space<gm>>,
                                                     %arg3 : memref<4x64x64xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST3:.*]] = arith.constant 67584 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 2048 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 1024 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<4x1x64xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<4x1x64xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<4x1x64xf32, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<4x1x64xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<4x1x64xf32, #hivm.address_space<gm>>) outs(%alloc : memref<4x1x64xf32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<4x1x64xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<4x1x64xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<4x64x64xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST3]]) : memref<4096xf32, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<4x64x64xf32, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<4096xf32, #hivm.address_space<ub>>
    hivm.hir.vmul ins(%alloc, %alloc_0 : memref<4x1x64xf32, #hivm.address_space<ub>>, memref<4x1x64xf32, #hivm.address_space<ub>>)
      outs(%alloc_1 : memref<4x64x64xf32, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<4096xf32, #hivm.address_space<ub>>) broadcast = [1]
    hivm.hir.store ins(%alloc_1 : memref<4x64x64xf32, #hivm.address_space<ub>>) outs(%arg3 : memref<4x64x64xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vmul_dyn_same_shape_inplace
  func.func @test_vmul_dyn_same_shape_inplace(%arg1 : memref<?xi16, #hivm.address_space<gm>>,
                                              %arg2 : memref<?xi16, #hivm.address_space<gm>>,
                                              %arg3 : memref<?xi16, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 1024 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 512 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: %[[CST256:.*]] = arith.constant 256 : index
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<256xi16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<256xi16, #hivm.address_space<ub>>
    %c256 = arith.constant 256 : index
    %alloc = memref.alloc() : memref<256xi16, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<256xi16, #hivm.address_space<ub>>
    %subview = memref.subview %alloc[0] [%c256] [1] : memref<256xi16, #hivm.address_space<ub>> to memref<?xi16, #hivm.address_space<ub>>
    %subview_0 = memref.subview %alloc_0[0] [%c256] [1] : memref<256xi16, #hivm.address_space<ub>> to memref<?xi16, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<?xi16, #hivm.address_space<gm>>) outs(%subview : memref<?xi16, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<?xi16, #hivm.address_space<gm>>) outs(%subview_0 : memref<?xi16, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<256xi16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<256xi16, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<256xi16, #hivm.address_space<ub>>
    %subview_1 = memref.subview %alloc_1[0] [%c256] [1] : memref<256xi16, #hivm.address_space<ub>> to memref<?xi16, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<256xi16, #hivm.address_space<ub>>
    hivm.hir.vmul ins(%subview, %subview_0 : memref<?xi16, #hivm.address_space<ub>>, memref<?xi16, #hivm.address_space<ub>>)
      outs(%subview_1 : memref<?xi16, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<256xi16, #hivm.address_space<ub>>) broadcast = [0]
    hivm.hir.store ins(%subview_1 : memref<?xi16, #hivm.address_space<ub>>) outs(%arg3 : memref<?xi16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vmul_dyn_same_2d_shape_inplace
  func.func @test_vmul_dyn_same_2d_shape_inplace(%arg1 : memref<?x?xi16, #hivm.address_space<gm>>,
                                                 %arg2 : memref<?x?xi16, #hivm.address_space<gm>>,
                                                 %arg3 : memref<?x?xi16, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 16384 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: %[[CST16:.*]] = arith.constant 16 : index
    // CHECK: %[[CST256:.*]] = arith.constant 256 : index
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<256x16xi16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<256x16xi16, #hivm.address_space<ub>>
    %c16 = arith.constant 16 : index
    %c256 = arith.constant 256 : index
    %alloc = memref.alloc() : memref<256x16xi16, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<256x16xi16, #hivm.address_space<ub>>
    %subview = memref.subview %alloc[0, 0] [%c256, %c16] [1, 1] : memref<256x16xi16, #hivm.address_space<ub>> to memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>
    %subview_0 = memref.subview %alloc_0[0, 0] [%c256, %c16] [1, 1] : memref<256x16xi16, #hivm.address_space<ub>> to memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<?x?xi16, #hivm.address_space<gm>>) outs(%subview : memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<?x?xi16, #hivm.address_space<gm>>) outs(%subview_0 : memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<256x16xi16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<1024xi16, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<256x16xi16, #hivm.address_space<ub>>
    %subview_1 = memref.subview %alloc_1[0, 0] [%c256, %c16] [1, 1] : memref<256x16xi16, #hivm.address_space<ub>> to memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<1024xi16, #hivm.address_space<ub>>
    hivm.hir.vmul ins(%subview, %subview_0 : memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>, memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>)
      outs(%subview_1 : memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<1024xi16, #hivm.address_space<ub>>) broadcast = [0]
    hivm.hir.store ins(%subview_1 : memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>) outs(%arg3 : memref<?x?xi16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vmul_dyn_same_2d_shape_inplace_one
  func.func @test_vmul_dyn_same_2d_shape_inplace_one(%arg1 : memref<?x?xi16, #hivm.address_space<gm>>,
                                                     %arg2 : memref<?x?xi16, #hivm.address_space<gm>>,
                                                     %arg3 : memref<?x?xi16, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 8704 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 512 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: %[[CST1:.*]] = arith.constant 1 : index
    // CHECK: %[[CST16:.*]] = arith.constant 16 : index
    // CHECK: %[[CST256:.*]] = arith.constant 256 : index
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<256x1xi16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<256x16xi16, #hivm.address_space<ub>>
    %c1 = arith.constant 1 : index
    %c16 = arith.constant 16 : index
    %c256 = arith.constant 256 : index
    %alloc = memref.alloc() : memref<256x1xi16, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<256x16xi16, #hivm.address_space<ub>>
    %subview = memref.subview %alloc[0, 0] [%c256, %c1] [1, 1] : memref<256x1xi16, #hivm.address_space<ub>> to memref<?x?xi16, strided<[1, 1]>, #hivm.address_space<ub>>
    %subview_0 = memref.subview %alloc_0[0, 0] [%c256, %c16] [1, 1] : memref<256x16xi16, #hivm.address_space<ub>> to memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<?x?xi16, #hivm.address_space<gm>>) outs(%subview : memref<?x?xi16, strided<[1, 1]>, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<?x?xi16, #hivm.address_space<gm>>) outs(%subview_0 : memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<256x16xi16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<1024xi16, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<256x16xi16, #hivm.address_space<ub>>
    %subview_1 = memref.subview %alloc_1[0, 0] [%c256, %c16] [1, 1] : memref<256x16xi16, #hivm.address_space<ub>> to memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<1024xi16, #hivm.address_space<ub>>
    hivm.hir.vmul ins(%subview, %subview_0 : memref<?x?xi16, strided<[1, 1]>, #hivm.address_space<ub>>, memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>)
      outs(%subview_1 : memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<1024xi16, #hivm.address_space<ub>>) broadcast = [0]
    hivm.hir.store ins(%subview_1 : memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>) outs(%arg3 : memref<?x?xi16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vmul_dyn_diff_2d_shape_not_inplace
  func.func @test_vmul_dyn_diff_2d_shape_not_inplace(%arg1 : memref<?x?xi16, #hivm.address_space<gm>>,
                                                     %arg2 : memref<?x?xi16, #hivm.address_space<gm>>,
                                                     %arg3 : memref<?x?xi16, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST3:.*]] = arith.constant 9216 : i64
    // CHECK: %[[CONST2:.*]] = arith.constant 1024 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 512 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: %[[CST1:.*]] = arith.constant 1 : index
    // CHECK: %[[CST16:.*]] = arith.constant 16 : index
    // CHECK: %[[CST256:.*]] = arith.constant 256 : index
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<256x1xi16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<256x1xi16, #hivm.address_space<ub>>
    %c1 = arith.constant 1 : index
    %c16 = arith.constant 16 : index
    %c256 = arith.constant 256 : index
    %alloc = memref.alloc() : memref<256x1xi16, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<256x1xi16, #hivm.address_space<ub>>
    %subview = memref.subview %alloc[0, 0] [%c256, %c1] [1, 1] : memref<256x1xi16, #hivm.address_space<ub>> to memref<?x?xi16, strided<[1, 1]>, #hivm.address_space<ub>>
    %subview_0 = memref.subview %alloc_0[0, 0] [%c256, %c1] [1, 1] : memref<256x1xi16, #hivm.address_space<ub>> to memref<?x?xi16, strided<[1, 1]>, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<?x?xi16, #hivm.address_space<gm>>) outs(%subview : memref<?x?xi16, strided<[1, 1]>, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<?x?xi16, #hivm.address_space<gm>>) outs(%subview_0 : memref<?x?xi16, strided<[1, 1]>, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<256x16xi16, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST3]]) : memref<1024xi16, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<256x16xi16, #hivm.address_space<ub>>
    %subview_1 = memref.subview %alloc_1[0, 0] [%c256, %c16] [1, 1] : memref<256x16xi16, #hivm.address_space<ub>> to memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<1024xi16, #hivm.address_space<ub>>
    hivm.hir.vmul ins(%subview, %subview_0 : memref<?x?xi16, strided<[1, 1]>, #hivm.address_space<ub>>, memref<?x?xi16, strided<[1, 1]>, #hivm.address_space<ub>>)
      outs(%subview_1 : memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<1024xi16, #hivm.address_space<ub>>) broadcast = [0]
    hivm.hir.store ins(%subview_1 : memref<?x?xi16, strided<[16, 1]>, #hivm.address_space<ub>>) outs(%arg3 : memref<?x?xi16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vmul_one_dyn_same_2d_shape_inplace_one
  func.func @test_vmul_one_dyn_same_2d_shape_inplace_one(%arg1 : memref<4x?xi16, #hivm.address_space<gm>>,
                                                         %arg2 : memref<?x64xi16, #hivm.address_space<gm>>,
                                                         %arg3 : memref<4x?xi16, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST2:.*]] = arith.constant 1024 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 512 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<512xi8, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<512xi8, #hivm.address_space<ub>>
    %c1 = arith.constant 1 : index
    %c0 = arith.constant 0 : index
    %c8 = arith.constant 8 : index
    %dim = memref.dim %arg1, %c1 : memref<4x?xi16, #hivm.address_space<gm>>
    %dim_0 = memref.dim %arg2, %c0 : memref<?x64xi16, #hivm.address_space<gm>>
    %alloc = memref.alloc() : memref<512xi8, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<512xi8, #hivm.address_space<ub>>
    %view = memref.view %alloc[%c0][%dim] : memref<512xi8, #hivm.address_space<ub>> to memref<4x?xi16, #hivm.address_space<ub>>
    %view_0 = memref.view %alloc_0[%c0][%dim_0] : memref<512xi8, #hivm.address_space<ub>> to memref<?x64xi16, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<4x?xi16, #hivm.address_space<gm>>) outs(%view : memref<4x?xi16, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg2 : memref<?x64xi16, #hivm.address_space<gm>>) outs(%view_0 : memref<?x64xi16, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<512xi8, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<256xi16, #hivm.address_space<ub>>
    %alloc_1 = memref.alloc() : memref<512xi8, #hivm.address_space<ub>>
    %dim_1 = memref.dim %arg3, %c0 : memref<4x?xi16, #hivm.address_space<gm>>
    %view_1 = memref.view %alloc_1[%c0][%dim_1] : memref<512xi8, #hivm.address_space<ub>> to memref<4x?xi16, #hivm.address_space<ub>>
    %alloc_2 = memref.alloc() : memref<256xi16, #hivm.address_space<ub>>
    hivm.hir.vmul ins(%view, %view_0 : memref<4x?xi16, #hivm.address_space<ub>>, memref<?x64xi16, #hivm.address_space<ub>>)
      outs(%view_1 : memref<4x?xi16, #hivm.address_space<ub>>) temp_buffer(%alloc_2 : memref<256xi16, #hivm.address_space<ub>>) broadcast = [0]
    hivm.hir.store ins(%view_1 : memref<4x?xi16, #hivm.address_space<ub>>) outs(%arg3 : memref<4x?xi16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_select_cond_alias_not_inplace
  func.func @test_select_cond_alias_not_inplace(%arg0: memref<32xf32, #hivm.address_space<gm>>,
                                                %arg1: memref<32xf32, #hivm.address_space<gm>>,
                                                %arg2: i1,
                                                %arg3: memref<32xf32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: hivm.hir.pointer_cast(%[[CONST1:.*]])
    %alloc_0 = memref.alloc() : memref<32xf32, #hivm.address_space<ub>>
    // CHECK: hivm.hir.pointer_cast(%[[CONST2:.*]])
    %alloc_1 = memref.alloc() : memref<32xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<32xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<32xf32, #hivm.address_space<ub>>)
    hivm.hir.load ins(%arg1 : memref<32xf32, #hivm.address_space<gm>>) outs(%alloc_1 : memref<32xf32, #hivm.address_space<ub>>)
    %c128 = arith.constant 128 : index
    %c1 = arith.constant 1 : index
    %c0 = arith.constant 0 : index
    %0:2 = scf.for %arg4 = %c0 to %c128 step %c1 iter_args(%arg5 = %alloc_0, %arg6 = %alloc_1) -> (memref<32xf32, #hivm.address_space<ub>>, memref<32xf32, #hivm.address_space<ub>>) {
      %1 = arith.select %arg2, %arg5, %arg6 : memref<32xf32, #hivm.address_space<ub>>
      // CHECK: hivm.hir.pointer_cast(%[[CONST0:.*]])
      %alloc_2 = memref.alloc() : memref<32xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg3 : memref<32xf32, #hivm.address_space<gm>>) outs(%alloc_2 : memref<32xf32, #hivm.address_space<ub>>)
      %2 = arith.select %arg2, %alloc_2, %arg5 : memref<32xf32, #hivm.address_space<ub>>
      %3 = arith.select %arg2, %arg6, %alloc_2 : memref<32xf32, #hivm.address_space<ub>>
      // CHECK: hivm.hir.pointer_cast(%[[CONST1]])
      %alloc_3 = memref.alloc() : memref<32xf32, #hivm.address_space<ub>>
      hivm.hir.copy ins(%2 : memref<32xf32, #hivm.address_space<ub>>) outs(%alloc_3 : memref<32xf32, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.pointer_cast(%[[CONST2]])
      %alloc_4 = memref.alloc() : memref<32xf32, #hivm.address_space<ub>>
      hivm.hir.copy ins(%3 : memref<32xf32, #hivm.address_space<ub>>) outs(%alloc_4 : memref<32xf32, #hivm.address_space<ub>>)
      scf.yield %alloc_3, %alloc_4 : memref<32xf32, #hivm.address_space<ub>>, memref<32xf32, #hivm.address_space<ub>>
    }
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_scfwhileop
  func.func @test_scfwhileop(%arg0 : memref<16xf32, #hivm.address_space<gm>>,
                             %arg1 : memref<16xf32, #hivm.address_space<ub>>,
                             %arg2 : memref<16xf32, #hivm.address_space<gm>>) {
    // CHECK: %[[CONST1:.*]] = arith.constant 64 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: %[[CST0:.*]] = arith.constant 0 : i32
    // CHECK: %[[CST1:.*]] = arith.constant 1 : i32
    // CHECK: %[[CST100:.*]] = arith.constant 100 : i32
    // CHECK: hivm.hir.pointer_cast(%[[CONST0]]) : memref<16xf32, #hivm.address_space<ub>>
    %c0 = arith.constant 0 : i32
    %c1 = arith.constant 1 : i32
    %c100 = arith.constant 100 : i32
    %alloc_1 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
    %0 = scf.while (%arg3 = %c0) : (i32) -> i32 {
      %1 = arith.cmpi eq, %arg3, %c100 : i32
      scf.condition(%1) %arg3 : i32
    } do {
    ^bb0(%arg3: i32):
      // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<16xf32, #hivm.address_space<ub>>
      %alloc_0 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg0 : memref<16xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<16xf32, #hivm.address_space<ub>>)
      hivm.hir.vadd ins(%alloc_0, %arg1 : memref<16xf32, #hivm.address_space<ub>>, memref<16xf32, #hivm.address_space<ub>>) outs(%alloc_1 : memref<16xf32, #hivm.address_space<ub>>)
      %2 = arith.addi %arg3, %c1 : i32
      scf.yield %2 : i32
    }
    hivm.hir.store ins(%alloc_1 : memref<16xf32, #hivm.address_space<ub>>) outs(%arg2 : memref<16xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_scfwhileop_yield_inplace
  func.func @test_scfwhileop_yield_inplace(%arg0 : memref<16xf32, #hivm.address_space<gm>>,
                                           %arg1 : memref<16xf32, #hivm.address_space<gm>>,
                                           %arg2 : memref<16xf32, #hivm.address_space<gm>>,
                                           %arg3 : memref<16xf32, #hivm.address_space<gm>>,
                                           %arg4 : i32) {
    %c0 = arith.constant 0 : i32
    %c1 = arith.constant 1 : i32
    %c100 = arith.constant 100 : i32
    // CHECK: hivm.hir.pointer_cast(%[[CONST2:.*]]) : memref<16xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<16xf32, #hivm.address_space<gm>>) outs(%alloc : memref<16xf32, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.pointer_cast(%[[CONST0:.*]]) : memref<16xf32, #hivm.address_space<ub>>
    %alloc_0 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<16xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<16xf32, #hivm.address_space<ub>>)
    %0 = scf.while (%arg5 = %alloc) : (memref<16xf32, #hivm.address_space<ub>>) -> memref<16xf32, #hivm.address_space<ub>> {
      %1 = arith.cmpi eq, %arg4, %c100 : i32
      scf.condition(%1) %alloc_0 : memref<16xf32, #hivm.address_space<ub>>
    } do {
    ^bb0(%arg6: memref<16xf32, #hivm.address_space<ub>>):
      // CHECK: hivm.hir.pointer_cast(%[[CONST1:.*]]) : memref<16xf32, #hivm.address_space<ub>>
      %alloc_1 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg2 : memref<16xf32, #hivm.address_space<gm>>) outs(%alloc_1 : memref<16xf32, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<16xf32, #hivm.address_space<ub>>
      %alloc_2 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%arg6, %alloc_1 : memref<16xf32, #hivm.address_space<ub>>, memref<16xf32, #hivm.address_space<ub>>) outs(%alloc_2 : memref<16xf32, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<16xf32, #hivm.address_space<ub>>
      %alloc_3 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%arg6, %alloc_2 : memref<16xf32, #hivm.address_space<ub>>, memref<16xf32, #hivm.address_space<ub>>) outs(%alloc_3 : memref<16xf32, #hivm.address_space<ub>>)
      scf.yield %alloc_3 : memref<16xf32, #hivm.address_space<ub>>
    }
    hivm.hir.store ins(%0#0 : memref<16xf32, #hivm.address_space<ub>>) outs(%arg3 : memref<16xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_dowhile_yield_inplace
  func.func @test_dowhile_yield_inplace(%arg0 : memref<16xf32, #hivm.address_space<gm>>,
                                        %arg1 : memref<16xf32, #hivm.address_space<gm>>,
                                        %arg2 : memref<16xf32, #hivm.address_space<gm>>,
                                        %arg3 : memref<16xf32, #hivm.address_space<gm>>,
                                        %arg4 : i32) {
    %c0 = arith.constant 0 : i32
    %c1 = arith.constant 1 : i32
    %c100 = arith.constant 100 : i32
    // CHECK: hivm.hir.pointer_cast(%[[CONST2:.*]]) : memref<16xf32, #hivm.address_space<ub>>
    %alloc = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<16xf32, #hivm.address_space<gm>>) outs(%alloc : memref<16xf32, #hivm.address_space<ub>>)
    %0 = scf.while (%arg5 = %alloc) : (memref<16xf32, #hivm.address_space<ub>>) -> memref<16xf32, #hivm.address_space<ub>> {
      // CHECK: hivm.hir.pointer_cast(%[[CONST0:.*]]) : memref<16xf32, #hivm.address_space<ub>>
      %alloc_0 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg1 : memref<16xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<16xf32, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.pointer_cast(%[[CONST1:.*]]) : memref<16xf32, #hivm.address_space<ub>>
      %alloc_1 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg2 : memref<16xf32, #hivm.address_space<gm>>) outs(%alloc_1 : memref<16xf32, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.pointer_cast(%[[CONST1]]) : memref<16xf32, #hivm.address_space<ub>>
      %alloc_2 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%arg5, %alloc_1 : memref<16xf32, #hivm.address_space<ub>>, memref<16xf32, #hivm.address_space<ub>>) outs(%alloc_2 : memref<16xf32, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.pointer_cast(%[[CONST2]]) : memref<16xf32, #hivm.address_space<ub>>
      %alloc_3 = memref.alloc() : memref<16xf32, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%alloc_0, %alloc_2 : memref<16xf32, #hivm.address_space<ub>>, memref<16xf32, #hivm.address_space<ub>>) outs(%alloc_3 : memref<16xf32, #hivm.address_space<ub>>)
      %1 = arith.cmpi eq, %arg4, %c100 : i32
      scf.condition(%1) %alloc_3 : memref<16xf32, #hivm.address_space<ub>>
    } do {
    ^bb0(%arg6: memref<16xf32, #hivm.address_space<ub>>):
      scf.yield %arg6 : memref<16xf32, #hivm.address_space<ub>>
    }
    hivm.hir.store ins(%0#0 : memref<16xf32, #hivm.address_space<ub>>) outs(%arg3 : memref<16xf32, #hivm.address_space<gm>>)
    return
  }
}

// -----

// CHECK-LABEL: func.func @move_pointer_casts_to_after_region_front(
func.func @move_pointer_casts_to_after_region_front(%arg0: memref<8xf32, #hivm.address_space<gm>>,
                                   %arg1: memref<8xf32, #hivm.address_space<gm>>) {
  %true = arith.constant true
  // CHECK: scf.while
  %r = scf.while (%cond = %true) : (i1) -> i1 {
    scf.condition(%cond) %cond : i1
  } do {
  // CHECK: ^bb0
  ^bb0(%cin: i1):
    // CHECK-NEXT: %[[PCAST:.*]] = hivm.hir.pointer_cast({{.*}}) : memref<8xf32, #hivm.address_space<ub>>
    // CHECK-NEXT: annotation.mark %[[PCAST]] {hivm.multi_buffer = 2 : i32}
    %tmp = memref.alloc() : memref<8xf32, #hivm.address_space<ub>>
    annotation.mark %tmp {hivm.multi_buffer = 2 : i32} : memref<8xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<8xf32, #hivm.address_space<gm>>)
                  outs(%tmp : memref<8xf32, #hivm.address_space<ub>>)
    hivm.hir.store ins(%tmp : memref<8xf32, #hivm.address_space<ub>>)
                   outs(%arg1 : memref<8xf32, #hivm.address_space<gm>>)
    scf.yield %cin : i1
  }
  return
}

// -----

// CHECK-LABEL: func.func @move_pointer_casts_to_before_region_front(
func.func @move_pointer_casts_to_before_region_front(%arg0: memref<8xf32, #hivm.address_space<gm>>,
                                   %arg1: memref<8xf32, #hivm.address_space<gm>>) {
  %true = arith.constant true
  // CHECK: scf.while
  %r = scf.while (%cond = %true) : (i1) -> i1 {
    // CHECK-NEXT: %[[PCAST:.*]] = hivm.hir.pointer_cast({{.*}}) : memref<8xf32, #hivm.address_space<ub>>
    // CHECK-NEXT: annotation.mark %[[PCAST]] {hivm.multi_buffer = 2 : i32}
    %tmp = memref.alloc() : memref<8xf32, #hivm.address_space<ub>>
    annotation.mark %tmp {hivm.multi_buffer = 2 : i32} : memref<8xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<8xf32, #hivm.address_space<gm>>)
                  outs(%tmp : memref<8xf32, #hivm.address_space<ub>>)
    hivm.hir.store ins(%tmp : memref<8xf32, #hivm.address_space<ub>>)
                   outs(%arg1 : memref<8xf32, #hivm.address_space<gm>>)
    scf.condition(%cond) %cond : i1
  } do {
  // CHECK: ^bb0
  ^bb0(%cin: i1):
    scf.yield %cin : i1
  }
  return
}

// -----

module {
  // CHECK-LABEL: func.func @test_branchop
  func.func @test_branchop(%arg0: memref<16xf16, #hivm.address_space<gm>>,
                           %arg1: memref<16xf16, #hivm.address_space<gm>>,
                           %arg2: memref<16xf16, #hivm.address_space<gm>>,
                           %arg3: i1) {
    // CHECK-NOT: memref.alloc()
    // CHECK:  hivm.hir.pointer_cast(%[[CONST0:.*]])
    %alloc = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<16xf16, #hivm.address_space<gm>>) outs(%alloc : memref<16xf16, #hivm.address_space<ub>>)
    cf.cond_br %arg3, ^bb1(%alloc : memref<16xf16, #hivm.address_space<ub>>), ^bb2(%alloc : memref<16xf16, #hivm.address_space<ub>>)
    ^bb1(%arg10 : memref<16xf16, #hivm.address_space<ub>>):
      // CHECK: hivm.hir.pointer_cast(%[[CONST1:.*]])
      %alloc_0 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg1 : memref<16xf16, #hivm.address_space<gm>>) outs(%alloc_0 : memref<16xf16, #hivm.address_space<ub>>)
      // Conditional branch alias: yield buffer is not inplaced with the incoming arg.
      // CHECK: hivm.hir.pointer_cast(%[[CONST3:.*]])
      %alloc_1 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%arg10, %alloc_0 : memref<16xf16, #hivm.address_space<ub>>, memref<16xf16, #hivm.address_space<ub>>)
        outs(%alloc_1 : memref<16xf16, #hivm.address_space<ub>>)
      cf.br ^bb3(%alloc_1 : memref<16xf16, #hivm.address_space<ub>>)
    ^bb2(%arg11 : memref<16xf16, #hivm.address_space<ub>>):
      // CHECK: hivm.hir.pointer_cast(%[[CONST2:.*]])
      %alloc_2 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg1 : memref<16xf16, #hivm.address_space<gm>>) outs(%alloc_2 : memref<16xf16, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.pointer_cast(%[[CONST4:.*]])
      %alloc_3 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.vsub ins(%arg11, %alloc_2 : memref<16xf16, #hivm.address_space<ub>>, memref<16xf16, #hivm.address_space<ub>>)
        outs(%alloc_3 : memref<16xf16, #hivm.address_space<ub>>)
      cf.br ^bb3(%alloc_3 : memref<16xf16, #hivm.address_space<ub>>)
    ^bb3(%arg12 : memref<16xf16, #hivm.address_space<ub>>):
      // CHECK: hivm.hir.pointer_cast(%[[CONST5:.*]])
      %alloc_4 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%arg12, %arg12 : memref<16xf16, #hivm.address_space<ub>>, memref<16xf16, #hivm.address_space<ub>>)
                  outs(%alloc_4 : memref<16xf16, #hivm.address_space<ub>>)
    hivm.hir.store ins(%alloc_4 : memref<16xf16, #hivm.address_space<ub>>) outs(%arg2 : memref<16xf16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_branchop_inplace
  func.func @test_branchop_inplace(%arg0: memref<16xf16, #hivm.address_space<gm>>,
                                   %arg1: memref<16xf16, #hivm.address_space<gm>>,
                                   %arg2: memref<16xf16, #hivm.address_space<gm>>,
                                   %arg3: i1) {
    // CHECK-NOT: memref.alloc()
    // CHECK:  hivm.hir.pointer_cast(%[[CONST0:.*]])
    %alloc = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<16xf16, #hivm.address_space<gm>>) outs(%alloc : memref<16xf16, #hivm.address_space<ub>>)
    cf.cond_br %arg3, ^bb1(%alloc : memref<16xf16, #hivm.address_space<ub>>), ^bb2(%alloc : memref<16xf16, #hivm.address_space<ub>>)
    ^bb1(%arg10 : memref<16xf16, #hivm.address_space<ub>>):
      // CHECK: hivm.hir.pointer_cast(%[[CONST1:.*]])
      %alloc_0 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg1 : memref<16xf16, #hivm.address_space<gm>>) outs(%alloc_0 : memref<16xf16, #hivm.address_space<ub>>)
      // Conditional branch alias: yield buffer is not inplaced with the incoming arg.
      // CHECK: hivm.hir.pointer_cast(%[[CONST3:.*]])
      %alloc_1 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%arg10, %alloc_0 : memref<16xf16, #hivm.address_space<ub>>, memref<16xf16, #hivm.address_space<ub>>)
        outs(%alloc_1 : memref<16xf16, #hivm.address_space<ub>>)
      cf.br ^bb3(%alloc_1, %arg10 : memref<16xf16, #hivm.address_space<ub>>, memref<16xf16, #hivm.address_space<ub>>)
    ^bb2(%arg11 : memref<16xf16, #hivm.address_space<ub>>):
      // CHECK: hivm.hir.pointer_cast(%[[CONST2:.*]])
      %alloc_2 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg1 : memref<16xf16, #hivm.address_space<gm>>) outs(%alloc_2 : memref<16xf16, #hivm.address_space<ub>>)
      // CHECK: hivm.hir.pointer_cast(%[[CONST4:.*]])
      %alloc_3 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.vsub ins(%arg11, %alloc_2 : memref<16xf16, #hivm.address_space<ub>>, memref<16xf16, #hivm.address_space<ub>>)
        outs(%alloc_3 : memref<16xf16, #hivm.address_space<ub>>)
      cf.br ^bb3(%alloc_3, %arg11 : memref<16xf16, #hivm.address_space<ub>>, memref<16xf16, #hivm.address_space<ub>>)
    ^bb3(%arg12 : memref<16xf16, #hivm.address_space<ub>>, %arg13 : memref<16xf16, #hivm.address_space<ub>>):
      // CHECK: hivm.hir.pointer_cast(%[[CONST5:.*]])
      %alloc_4 = memref.alloc() : memref<16xf16, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%arg12, %arg13 : memref<16xf16, #hivm.address_space<ub>>, memref<16xf16, #hivm.address_space<ub>>)
                  outs(%alloc_4 : memref<16xf16, #hivm.address_space<ub>>)
      hivm.hir.store ins(%alloc_4 : memref<16xf16, #hivm.address_space<ub>>) outs(%arg2 : memref<16xf16, #hivm.address_space<gm>>)
      return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vsel_i64_reuse
  func.func @test_vsel_i64_reuse(%arg0: memref<16xi64, #hivm.address_space<gm>>,
                                   %arg1: memref<16xi64, #hivm.address_space<gm>>,
                                   %arg2: memref<16xi64, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST2:.*]] = arith.constant 256 : i64
    // CHECK: %[[CONST1:.*]] = arith.constant 128 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0_i64 = arith.constant 0 : i64
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
    %alloc = memref.alloc() : memref<16xi64, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<16xi64, #hivm.address_space<gm>>) outs(%alloc : memref<16xi64, #hivm.address_space<ub>>)
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
    %alloc_1 = memref.alloc() : memref<16xi64, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg1 : memref<16xi64, #hivm.address_space<gm>>) outs(%alloc_1 : memref<16xi64, #hivm.address_space<ub>>)
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST2]])
    %alloc_2 = memref.alloc() : memref<16xi1, #hivm.address_space<ub>>
    hivm.hir.vcmp ins(%alloc, %c0_i64 : memref<16xi64, #hivm.address_space<ub>>, i64) outs(%alloc_2 : memref<16xi1, #hivm.address_space<ub>>)
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
    %alloc_3 = memref.alloc() : memref<16xi64, #hivm.address_space<ub>>
    hivm.hir.vsel ins(%alloc_2, %alloc, %alloc_1 : memref<16xi1, #hivm.address_space<ub>>, memref<16xi64, #hivm.address_space<ub>>, memref<16xi64, #hivm.address_space<ub>>) outs(%alloc_3 : memref<16xi64, #hivm.address_space<ub>>)
    hivm.hir.store ins(%alloc_3 : memref<16xi64, #hivm.address_space<ub>>) outs(%arg2 : memref<16xi64, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vsel_i64_no_reuse
  func.func @test_vsel_i64_no_reuse(%arg0: memref<16xi64, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 256 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0_i64 = arith.constant 0 : i64
    %c1_i64 = arith.constant 1 : i64
    %c2_i64 = arith.constant 2 : i64
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
    %alloc = memref.alloc() : memref<2048xi1, #hivm.address_space<ub>>
    linalg.fill ins(%c1_i64: i64) outs(%alloc : memref<2048xi1, #hivm.address_space<ub>>)
    %subview = memref.subview %alloc[0] [16] [1] : memref<2048xi1, #hivm.address_space<ub>> to memref<16xi1, #hivm.address_space<ub>>
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
    %alloc_1 = memref.alloc() : memref<16xi64, #hivm.address_space<ub>>
    hivm.hir.vsel ins(%subview, %c1_i64, %c2_i64 : memref<16xi1, #hivm.address_space<ub>>, i64, i64) outs(%alloc_1 : memref<16xi64, #hivm.address_space<ub>>)
    hivm.hir.store ins(%alloc_1 : memref<16xi64, #hivm.address_space<ub>>) outs(%arg0 : memref<16xi64, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_vsel_i32_reuse_cond
  func.func @test_vsel_i32_reuse_cond(%arg0: memref<16xi32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    %c0_i32 = arith.constant 0 : i32
    %c1_i32 = arith.constant 1 : i32
    %c2_i32 = arith.constant 2 : i32
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
    %alloc = memref.alloc() : memref<2048xi1, #hivm.address_space<ub>>
    linalg.fill ins(%c1_i32: i32) outs(%alloc : memref<2048xi1, #hivm.address_space<ub>>)
    %subview = memref.subview %alloc[0] [16] [1] : memref<2048xi1, #hivm.address_space<ub>> to memref<16xi1, #hivm.address_space<ub>>
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
    %alloc_1 = memref.alloc() : memref<16xi32, #hivm.address_space<ub>>
    hivm.hir.vsel ins(%subview, %c1_i32, %c2_i32 : memref<16xi1, #hivm.address_space<ub>>, i32, i32) outs(%alloc_1 : memref<16xi32, #hivm.address_space<ub>>)
    hivm.hir.store ins(%alloc_1 : memref<16xi32, #hivm.address_space<ub>>) outs(%arg0 : memref<16xi32, #hivm.address_space<gm>>)
    return
  }
}

// -----
module {
  // CHECK-LABEL: func.func @test_vabs_offset_unequal_not_inplace
  func.func @test_vabs_offset_unequal_not_inplace(%arg0: memref<8x16x32xf16, #hivm.address_space<gm>>, %arg1: memref<7x13x13xf16, #hivm.address_space<gm>>) attributes {hacc.entry, hacc.function_kind = #hacc.function_kind<DEVICE>, hivm.func_core_type = #hivm.func_core_type<AIV>, hivm.storage_aligned} {
    hivm.hir.set_mask_norm
    // CHECK-NOT: memref.alloc()
    // CHECK: %[[CONST1:.*]] = arith.constant 8192 : i64
    // CHECK: %[[CONST0:.*]] = arith.constant 0 : i64
    // CHECK: %[[CST0:.*]] = hivm.hir.pointer_cast(%[[CONST0]]) : memref<8x16x32xf16, #hivm.address_space<ub>>
    // CHECK: %[[SUBVIEW0:.*]] = memref.subview %[[CST0]][0, 1, 4] [7, 13, 13] [1, 1, 2]
    // CHECK: %[[CST1:.*]] = hivm.hir.pointer_cast(%[[CONST1]]) : memref<8x16x32xf16, #hivm.address_space<ub>>
    // CHECK: %[[SUBVIEW1:.*]] = memref.subview %[[CST1]][1, 3, 6] [7, 13, 13] [1, 1, 2]
    // CHECK: hivm.hir.vabs ins(%[[SUBVIEW0]] : memref<7x13x13xf16, strided<[512, 32, 2], offset: 36>, #hivm.address_space<ub>>) outs(%[[SUBVIEW1]] : memref<7x13x13xf16, strided<[512, 32, 2], offset: 614>, #hivm.address_space<ub>>)

    %alloc = memref.alloc() : memref<8x16x32xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<8x16x32xf16, #hivm.address_space<gm>>) outs(%alloc : memref<8x16x32xf16, #hivm.address_space<ub>>)
    %subview = memref.subview %alloc[0, 1, 4] [7, 13, 13] [1, 1, 2] : memref<8x16x32xf16, #hivm.address_space<ub>> to memref<7x13x13xf16, strided<[512, 32, 2], offset: 36>, #hivm.address_space<ub>>

    %alloc_1 = memref.alloc() : memref<8x16x32xf16, #hivm.address_space<ub>>
    %subview_2 = memref.subview %alloc_1[1, 3, 6] [7, 13, 13] [1, 1, 2] : memref<8x16x32xf16, #hivm.address_space<ub>> to memref<7x13x13xf16, strided<[512, 32, 2], offset: 614>, #hivm.address_space<ub>>
    hivm.hir.vabs ins(%subview : memref<7x13x13xf16, strided<[512, 32, 2], offset: 36>, #hivm.address_space<ub>>) outs(%subview_2 : memref<7x13x13xf16, strided<[512, 32, 2], offset: 614>, #hivm.address_space<ub>>)
    hivm.hir.store ins(%subview_2 : memref<7x13x13xf16, strided<[512, 32, 2], offset: 614>, #hivm.address_space<ub>>) outs(%arg1 : memref<7x13x13xf16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // expected-error@below {{ub overflow, requires 1835008 bits while 1572864 bits available! (possible reason: tiling basic block is too large or block number is more than what user expect due to multi-buffer feature is enabled and some ops need extra local buffer.)}}
  func.func @test_preload_buffer_expand_lifetime(%arg0: memref<16384xi8, #hivm.address_space<gm>>,
                                                 %arg1: memref<16384xi32, #hivm.address_space<gm>>,
                                                 %arg2: memref<16384xi32, #hivm.address_space<gm>>,
                                                 %arg3: memref<4096xi32, #hivm.address_space<gm>>,
                                                 %arg4: memref<4096xi32, #hivm.address_space<gm>>,
                                                 %arg5: memref<4096xi32, #hivm.address_space<gm>>) {
    %c4 = arith.constant 4 : index
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 1 : index
    %c0_i32 = arith.constant 0 : i32
    %c1_i32 = arith.constant 1 : i32
    %c16_i32 = arith.constant 16 : i32
    %c128_i32 = arith.constant 128 : i32
    %cst_0 = arith.constant 0xFF8000 : i32
    %alloc = memref.alloc() : memref<4096xi32, #hivm.address_space<ub>>
    hivm.hir.vbrc ins(%cst_0 : i32) outs(%alloc : memref<4096xi32, #hivm.address_space<ub>>)
    scf.for %arg6 = %c0_i32 to %c16_i32 step %c1_i32  : i32 {
      %alloc_0 = memref.alloc() : memref<4096xi32, #hivm.address_space<ub>>
      hivm.hir.copy ins(%alloc : memref<4096xi32, #hivm.address_space<ub>>) outs(%alloc_0 : memref<4096xi32, #hivm.address_space<ub>>)
      %0 = scf.for %arg7 = %c0_i32 to %c128_i32 step %c16_i32 iter_args(%arg8 = %alloc_0) -> (memref<4096xi32, #hivm.address_space<ub>>)  : i32 {
          %1 = scope.scope : () -> (memref<4096xi32, #hivm.address_space<ub>>) {
          %alloc_1 = memref.alloc() : memref<16384xi8, #hivm.address_space<ub>>
          hivm.hir.load ins(%arg0 : memref<16384xi8, #hivm.address_space<gm>>) outs(%alloc_1 : memref<16384xi8, #hivm.address_space<ub>>)
          %alloc_2 = memref.alloc() : memref<16384xi16, #hivm.address_space<ub>>
          hivm.hir.vcast ins(%alloc_1 : memref<16384xi8, #hivm.address_space<ub>>) outs(%alloc_2 : memref<16384xi16, #hivm.address_space<ub>>)
          %alloc_3 = memref.alloc() : memref<16384xi32, #hivm.address_space<ub>>
          hivm.hir.vcast ins(%alloc_2 : memref<16384xi16, #hivm.address_space<ub>>) outs(%alloc_3 : memref<16384xi32, #hivm.address_space<ub>>)
          %alloc_4 = memref.alloc() : memref<16384xi32, #hivm.address_space<ub>>
          hivm.hir.load ins(%arg1 : memref<16384xi32, #hivm.address_space<gm>>) outs(%alloc_4 : memref<16384xi32, #hivm.address_space<ub>>)
          %alloc_5 = memref.alloc() : memref<16384xi32, #hivm.address_space<ub>>
          hivm.hir.vadd ins(%alloc_3, %alloc_4 : memref<16384xi32, #hivm.address_space<ub>>, memref<16384xi32, #hivm.address_space<ub>>) outs(%alloc_5 : memref<16384xi32, #hivm.address_space<ub>>)
          hivm.hir.store ins(%alloc_5 : memref<16384xi32, #hivm.address_space<ub>>) outs(%arg2 : memref<16384xi32, #hivm.address_space<gm>>)
          %alloc_6 = memref.alloc() : memref<4096xi32, #hivm.address_space<ub>>
          annotation.mark %alloc_6 {hivm.multi_buffer = 4 : i32, hivm.preload_local_buffer = 1 : i32} : memref<4096xi32, #hivm.address_space<ub>>
          scf.for %arg9 = %c0 to %c4 step %c1 {
            %alloc_7 = memref.alloc() : memref<4096xi32, #hivm.address_space<ub>>
            hivm.hir.load ins(%arg3 : memref<4096xi32, #hivm.address_space<gm>>) outs(%alloc_7 : memref<4096xi32, #hivm.address_space<ub>>)
            hivm.hir.vmax ins(%arg8, %alloc_7 : memref<4096xi32, #hivm.address_space<ub>>, memref<4096xi32, #hivm.address_space<ub>>) outs(%alloc_6 : memref<4096xi32, #hivm.address_space<ub>>)
          }
          scope.return %alloc_6 : memref<4096xi32, #hivm.address_space<ub>>
        } {hivm.loop_core_type = #hivm.tcore_type<VECTOR>, hivm.preload_num = 2 : i32, no_inline}
        %2 = scope.scope : () -> memref<4096xi32, #hivm.address_space<ub>> {
          %alloc_1 = memref.alloc() : memref<4096xi32, #hivm.address_space<ub>>
          hivm.hir.load ins(%arg4 : memref<4096xi32, #hivm.address_space<gm>>) outs(%alloc_1 : memref<4096xi32, #hivm.address_space<ub>>)
          %alloc_2 = memref.alloc() : memref<4096xi32, #hivm.address_space<ub>>
          hivm.hir.vadd ins(%1, %alloc_1 : memref<4096xi32, #hivm.address_space<ub>>, memref<4096xi32, #hivm.address_space<ub>>) outs(%alloc_2 : memref<4096xi32, #hivm.address_space<ub>>)
          scope.return %alloc_2 : memref<4096xi32, #hivm.address_space<ub>>
        } {hivm.loop_core_type = #hivm.tcore_type<VECTOR>, hivm.preload_num = 0 : i32, no_inline}
        scf.yield %2 : memref<4096xi32, #hivm.address_space<ub>>
      }
      hivm.hir.store ins(%0 : memref<4096xi32, #hivm.address_space<ub>>) outs(%arg5 : memref<4096xi32, #hivm.address_space<gm>>)
    }
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_dead_after_op_nested_if_in_for
  func.func @test_dead_after_op_nested_if_in_for(
      %src: memref<16x16xf16, #hivm.address_space<gm>>,
      %dst1: memref<16x16xf16, #hivm.address_space<gm>>,
      %dst2: memref<256xf16, #hivm.address_space<gm>>,
      %cond: i1) {
    // CHECK-NOT: memref.alloc()
    %alloc = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
    %collapse = memref.collapse_shape %alloc [[0, 1]] :
        memref<16x16xf16, #hivm.address_space<ub>> into memref<256xf16, #hivm.address_space<ub>>
    hivm.hir.load ins(%src : memref<16x16xf16, #hivm.address_space<gm>>)
                  outs(%alloc : memref<16x16xf16, #hivm.address_space<ub>>)

    // CHECK: hivm.hir.pointer_cast(%[[CONST0:.*]])
    // CHECK: hivm.hir.pointer_cast(%[[CONST1:.*]])
    %if0 = scf.if %cond -> (memref<16x16xf16, #hivm.address_space<ub>>) {
      %tmp0 = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%alloc, %alloc : memref<16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16xf16, #hivm.address_space<ub>>)
                  outs(%tmp0 : memref<16x16xf16, #hivm.address_space<ub>>)
      scf.yield %tmp0 : memref<16x16xf16, #hivm.address_space<ub>>
    } else {
      scf.yield %alloc : memref<16x16xf16, #hivm.address_space<ub>>
    }

    %c0 = arith.constant 0 : index
    %c1024 = arith.constant 1024 : index
    %c128 = arith.constant 128 : index

    // CHECK: hivm.hir.pointer_cast(%[[CONST2:.*]])
    scf.for %iv = %c0 to %c1024 step %c128 {
      %if1 = scf.if %cond -> (memref<16x16xf16, #hivm.address_space<ub>>) {
        scf.yield %alloc : memref<16x16xf16, #hivm.address_space<ub>>
      } else {
        %local = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
        hivm.hir.vadd ins(%if0, %if0 : memref<16x16xf16, #hivm.address_space<ub>>,
                          memref<16x16xf16, #hivm.address_space<ub>>)
                    outs(%local : memref<16x16xf16, #hivm.address_space<ub>>)
        scf.yield %local : memref<16x16xf16, #hivm.address_space<ub>>
      }
      hivm.hir.vadd ins(%if1, %if1 : memref<16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16xf16, #hivm.address_space<ub>>)
                  outs(%alloc : memref<16x16xf16, #hivm.address_space<ub>>)
    }

    // CHECK: hivm.hir.pointer_cast(%[[CONST3:.*]])
    %if2 = scf.if %cond -> (memref<16x16xf16, #hivm.address_space<ub>>) {
      %tmp2 = memref.alloc() : memref<16x16xf16, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%alloc, %alloc : memref<16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16xf16, #hivm.address_space<ub>>)
                  outs(%tmp2 : memref<16x16xf16, #hivm.address_space<ub>>)
      scf.yield %tmp2 : memref<16x16xf16, #hivm.address_space<ub>>
    } else {
      scf.yield %alloc : memref<16x16xf16, #hivm.address_space<ub>>
    }

    scf.for %iv2 = %c0 to %c1024 step %c128 {
      hivm.hir.vadd ins(%if2, %if2 : memref<16x16xf16, #hivm.address_space<ub>>,
                        memref<16x16xf16, #hivm.address_space<ub>>)
                  outs(%alloc : memref<16x16xf16, #hivm.address_space<ub>>)
    }

    hivm.hir.store ins(%alloc : memref<16x16xf16, #hivm.address_space<ub>>)
                   outs(%dst1 : memref<16x16xf16, #hivm.address_space<gm>>)
    hivm.hir.store ins(%collapse : memref<256xf16, #hivm.address_space<ub>>)
                   outs(%dst2 : memref<256xf16, #hivm.address_space<gm>>)
    return
  }
}

// -----

module {
  // CHECK-LABEL: func.func @test_plan_memory_for_alias
  func.func @test_plan_memory_for_alias(%arg0: memref<64xf32, #hivm.address_space<gm>>, %arg1: memref<64xf32, #hivm.address_space<gm>>, %arg2: memref<64xf32, #hivm.address_space<gm>>, %arg3: i1, %arg4: memref<1xf32, #hivm.address_space<gm>>) {
    // CHECK-NOT: memref.alloc()
    // CHECK: hivm.hir.pointer_cast
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 1 : index
    %c64 = arith.constant 64 : index
    %alloc = memref.alloc() : memref<64xf32, #hivm.address_space<ub>>
    hivm.hir.load ins(%arg0 : memref<64xf32, #hivm.address_space<gm>>) outs(%alloc : memref<64xf32, #hivm.address_space<ub>>)
    %0 = scf.for %arg5 = %c0 to %c64 step %c1 iter_args(%arg6 = %alloc) -> (memref<64xf32, #hivm.address_space<ub>>) {
      %1 = scf.for %arg7 = %c0 to %c64 step %c1 iter_args(%arg8 = %arg6) -> (memref<64xf32, #hivm.address_space<ub>>) {
        %alloc_0 = memref.alloc() : memref<64xf32, #hivm.address_space<ub>>
        hivm.hir.copy ins(%arg8 : memref<64xf32, #hivm.address_space<ub>>) outs(%alloc_0 : memref<64xf32, #hivm.address_space<ub>>)
        %alloc_1 = memref.alloc() : memref<64xf32, #hivm.address_space<ub>>
        hivm.hir.copy ins(%alloc_0 : memref<64xf32, #hivm.address_space<ub>>) outs(%alloc_1 : memref<64xf32, #hivm.address_space<ub>>)
        scf.yield %alloc_1 : memref<64xf32, #hivm.address_space<ub>>
      }
      %alloc_0 = memref.alloc() : memref<64xf32, #hivm.address_space<ub>>
      hivm.hir.load ins(%arg1 : memref<64xf32, #hivm.address_space<gm>>) outs(%alloc_0 : memref<64xf32, #hivm.address_space<ub>>)
      %alloc_1 = memref.alloc() : memref<64xf32, #hivm.address_space<ub>>
      hivm.hir.vadd ins(%1, %alloc_0 : memref<64xf32, #hivm.address_space<ub>>,
                        memref<64xf32, #hivm.address_space<ub>>)
                  outs(%alloc_1 : memref<64xf32, #hivm.address_space<ub>>)
      scf.yield %alloc_1 : memref<64xf32, #hivm.address_space<ub>>
    }
    scf.for %arg9 = %c0 to %c64 step %c1 {
      %2 = arith.index_cast %arg9 : index to i64
      %3 = memref.load %0[%arg9] : memref<64xf32, #hivm.address_space<ub>>
      %alloc_0 = memref.alloc() : memref<1xf32, #hivm.address_space<ub>>
      memref.store %3, %alloc_0[%c0] : memref<1xf32, #hivm.address_space<ub>>
      hivm.hir.store ins(%alloc_0 : memref<1xf32, #hivm.address_space<ub>>)
                   outs(%arg4 : memref<1xf32, #hivm.address_space<gm>>)
    }
    return
  }
}

// -----

module {
  // This test guards preload local buffer liveness extension.
  //
  // The two preload local buffers are allocated inside two sibling scf.for loops.
  // Each alloc is inside a scope.scope.
  //
  // The important property is that each preload buffer must be tracked by its
  // own enclosing loop:
  //
  //   loop0 -> buf0
  //   loop1 -> buf1
  //
  // Instead of being globally extended to every preload loop:
  //
  //   loop0 -> buf0, buf1
  //   loop1 -> buf0, buf1
  //
  // The buffer is intentionally large. If the sibling-loop preload buffers are
  // incorrectly treated as live in the same loop, memory planning may require
  // both multi-buffer carriers at the same time and fail.

  // CHECK-LABEL: func.func @test_preload_local_buffer_lifetime_is_per_enclosing_loop
  func.func @test_preload_local_buffer_lifetime_is_per_enclosing_loop(
      %src0: memref<81920xi8, #hivm.address_space<gm>>,
      %dst0: memref<81920xi8, #hivm.address_space<gm>>,
      %src1: memref<81920xi8, #hivm.address_space<gm>>,
      %dst1: memref<81920xi8, #hivm.address_space<gm>>) {
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 1 : index
    %c4 = arith.constant 4 : index

    // First preload loop.
    //
    // CHECK: scf.for
    // CHECK: %[[P0:.*]] = hivm.hir.pointer_cast({{.*}}) : memref<81920xi8, #hivm.address_space<ub>>
    // CHECK: annotation.mark %[[P0]] {{.*}}hivm.multi_buffer = 2 : i32{{.*}}hivm.preload_local_buffer = 1 : i32{{.*}}
    // CHECK: scope.scope
    // CHECK: hivm.hir.load ins(%arg0 : memref<81920xi8, #hivm.address_space<gm>>) outs(%[[P0]] : memref<81920xi8, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.store ins(%[[P0]] : memref<81920xi8, #hivm.address_space<ub>>) outs(%arg1 : memref<81920xi8, #hivm.address_space<gm>>)
    // CHECK: scope.return
    scf.for %i = %c0 to %c4 step %c1 {
      scope.scope : () -> () {
        %buf0 = memref.alloc() : memref<81920xi8, #hivm.address_space<ub>>
        annotation.mark %buf0 {
          hivm.multi_buffer = 2 : i32,
          hivm.preload_local_buffer = 1 : i32
        } : memref<81920xi8, #hivm.address_space<ub>>

        hivm.hir.load ins(%src0 : memref<81920xi8, #hivm.address_space<gm>>)
                      outs(%buf0 : memref<81920xi8, #hivm.address_space<ub>>)
        hivm.hir.store ins(%buf0 : memref<81920xi8, #hivm.address_space<ub>>)
                       outs(%dst0 : memref<81920xi8, #hivm.address_space<gm>>)

        scope.return
      }
    }

    // Second sibling preload loop.
    //
    // This alloc is in a different enclosing loop. The liveness extension should
    // start and end at this second loop, not at the first loop.
    //
    // CHECK: scf.for
    // CHECK: %[[P1:.*]] = hivm.hir.pointer_cast({{.*}}) : memref<81920xi8, #hivm.address_space<ub>>
    // CHECK: annotation.mark %[[P1]] {{.*}}hivm.multi_buffer = 2 : i32{{.*}}hivm.preload_local_buffer = 1 : i32{{.*}}
    // CHECK: scope.scope
    // CHECK: hivm.hir.load ins(%arg2 : memref<81920xi8, #hivm.address_space<gm>>) outs(%[[P1]] : memref<81920xi8, #hivm.address_space<ub>>)
    // CHECK: hivm.hir.store ins(%[[P1]] : memref<81920xi8, #hivm.address_space<ub>>) outs(%arg3 : memref<81920xi8, #hivm.address_space<gm>>)
    // CHECK: scope.return
    scf.for %j = %c0 to %c4 step %c1 {
      scope.scope : () -> () {
        %buf1 = memref.alloc() : memref<81920xi8, #hivm.address_space<ub>>
        annotation.mark %buf1 {
          hivm.multi_buffer = 2 : i32,
          hivm.preload_local_buffer = 1 : i32
        } : memref<81920xi8, #hivm.address_space<ub>>

        hivm.hir.load ins(%src1 : memref<81920xi8, #hivm.address_space<gm>>)
                      outs(%buf1 : memref<81920xi8, #hivm.address_space<ub>>)
        hivm.hir.store ins(%buf1 : memref<81920xi8, #hivm.address_space<ub>>)
                       outs(%dst1 : memref<81920xi8, #hivm.address_space<gm>>)

        scope.return
      }
    }

    // CHECK: return
    return
  }
}

// -----
func.func @test_reuse_l0C(%arg0: memref<128x128xf16, #hivm.address_space<gm>>, %arg1: memref<128x128xf16, #hivm.address_space<gm>>, %arg2: i1) {
  %c1 = arith.constant 1 : index
  %c64 = arith.constant 64 : index
  %c128 = arith.constant 128 : index
  %c0 = arith.constant 0 : index
  %alloc = memref.alloc() {alignment = 64 : i64} : memref<128x128xf16, #hivm.address_space<cbuf>>
  hivm.hir.load ins(%arg0 : memref<128x128xf16, #hivm.address_space<gm>>) outs(%alloc : memref<128x128xf16, #hivm.address_space<cbuf>>)
  %alloc_0 = memref.alloc() {alignment = 64 : i64} : memref<128x128xf16, #hivm.address_space<cbuf>>
  hivm.hir.load ins(%arg1 : memref<128x128xf16, #hivm.address_space<gm>>) outs(%alloc_0 : memref<128x128xf16, #hivm.address_space<cbuf>>)
  // CHECK: scf.for
  scf.for %arg3 = %c0 to %c64 step %c1 {
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0:.*]], %[[CONST1:.*]])
    // CHECK-NOT: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]])
    scf.if %arg2 {
      %alloc_2 = memref.alloc() {alignment = 64 : i64} : memref<128x128xf16, #hivm.address_space<cc>>
      annotation.mark %alloc_2 {hivm.multi_buffer = 2 : i32} : memref<128x128xf16, #hivm.address_space<cc>>
      hivm.hir.mmadL1 ins(%alloc, %alloc_0, %arg2, %c128, %c128, %c128 :
                          memref<128x128xf16, #hivm.address_space<cbuf>>, memref<128x128xf16, #hivm.address_space<cbuf>>, i1, index, index, index)
                    outs(%alloc_2 : memref<128x128xf16, #hivm.address_space<cc>>)
      hivm.hir.fixpipe ins(%alloc_2 : memref<128x128xf16, #hivm.address_space<cc>>) outs(%arg0 : memref<128x128xf16, #hivm.address_space<gm>>)
    }
    scf.if %arg2 {
      %alloc_2 = memref.alloc() {alignment = 64 : i64} : memref<128x128xf16, #hivm.address_space<cc>>
      annotation.mark %alloc_2 {hivm.multi_buffer = 2 : i32} : memref<128x128xf16, #hivm.address_space<cc>>
      hivm.hir.mmadL1 ins(%alloc, %alloc_0, %arg2, %c128, %c128, %c128 :
                          memref<128x128xf16, #hivm.address_space<cbuf>>, memref<128x128xf16, #hivm.address_space<cbuf>>, i1, index, index, index)
                    outs(%alloc_2 : memref<128x128xf16, #hivm.address_space<cc>>)
      hivm.hir.fixpipe ins(%alloc_2 : memref<128x128xf16, #hivm.address_space<cc>>) outs(%arg0 : memref<128x128xf16, #hivm.address_space<gm>>)
    }
  }
  // CHECK: scf.for
  scf.for %arg3 = %c0 to %c64 step %c1 {
    // CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0:.*]], %[[CONST1:.*]])
    // CHECK-NOT: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]], %[[CONST1]])
    %alloc_1 = memref.alloc() {alignment = 64 : i64} : memref<128x128xf16, #hivm.address_space<cc>>
    annotation.mark %alloc_1 {hivm.multi_buffer = 2 : i32} : memref<128x128xf16, #hivm.address_space<cc>>
    hivm.hir.mmadL1 ins(%alloc, %alloc_0, %arg2, %c128, %c128, %c128 :
                        memref<128x128xf16, #hivm.address_space<cbuf>>, memref<128x128xf16, #hivm.address_space<cbuf>>, i1, index, index, index)
                  outs(%alloc_1 : memref<128x128xf16, #hivm.address_space<cc>>)
    hivm.hir.fixpipe ins(%alloc_1 : memref<128x128xf16, #hivm.address_space<cc>>) outs(%arg0 : memref<128x128xf16, #hivm.address_space<gm>>)
    %alloc_2 = memref.alloc() {alignment = 64 : i64} : memref<128x128xf16, #hivm.address_space<cc>>
    annotation.mark %alloc_2 {hivm.multi_buffer = 2 : i32} : memref<128x128xf16, #hivm.address_space<cc>>
    hivm.hir.mmadL1 ins(%alloc, %alloc_0, %arg2, %c128, %c128, %c128 :
                        memref<128x128xf16, #hivm.address_space<cbuf>>, memref<128x128xf16, #hivm.address_space<cbuf>>, i1, index, index, index)
                  outs(%alloc_2 : memref<128x128xf16, #hivm.address_space<cc>>)
    hivm.hir.fixpipe ins(%alloc_2 : memref<128x128xf16, #hivm.address_space<cc>>) outs(%arg0 : memref<128x128xf16, #hivm.address_space<gm>>)
  }
  return
}

// -----
// CHECK: warning: [hivm-plan-memory] There reused some dma buffers in ub address space, which may stall pipe. Not reusing dma buffer needs 2097152 bits while 1572864 bits available!
func.func @test_reuse_dma_buffer_warning(%arg0: memref<16x32x128xf16, #hivm.address_space<gm>>, %arg1: memref<16x32x128xf16, #hivm.address_space<gm>>) {
  %alloc = memref.alloc() : memref<16x32x128xf16, #hivm.address_space<ub>>
  hivm.hir.load ins(%arg0 : memref<16x32x128xf16, #hivm.address_space<gm>>) outs(%alloc : memref<16x32x128xf16, #hivm.address_space<ub>>)
  hivm.hir.store ins(%alloc : memref<16x32x128xf16, #hivm.address_space<ub>>) outs(%arg0 : memref<16x32x128xf16, #hivm.address_space<gm>>)
  %alloc_0 = memref.alloc() : memref<16x32x128xf16, #hivm.address_space<ub>>
  hivm.hir.load ins(%arg1 : memref<16x32x128xf16, #hivm.address_space<gm>>) outs(%alloc_0 : memref<16x32x128xf16, #hivm.address_space<ub>>)
  hivm.hir.store ins(%alloc_0 : memref<16x32x128xf16, #hivm.address_space<ub>>) outs(%arg1 : memref<16x32x128xf16, #hivm.address_space<gm>>)
  return
}

// -----
// This test demonstrates that reordering the memory plan can reduce the total UB usage.

// CHECK-LABEL: func.func @test_change_mem_plan_order
// CHECK: %[[CONST0:.*]] = arith.constant 147456 : i64
// CHECK: %[[CONST1:.*]] = arith.constant 16384 : i64
// CHECK: %[[CONST2:.*]] = arith.constant 0 : i64
// CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST2]])
// CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
// CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
// CHECK: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])

// CHECK-REORDER-LABEL: func.func @test_change_mem_plan_order
// CHECK-REORDER: %[[CONST0:.*]] = arith.constant 0 : i64
// CHECK-REORDER: %[[CONST1:.*]] = arith.constant 131072 : i64
// CHECK-REORDER: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
// CHECK-REORDER: {{.*}} = hivm.hir.pointer_cast(%[[CONST0]])
// CHECK-REORDER: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
// CHECK-REORDER: {{.*}} = hivm.hir.pointer_cast(%[[CONST1]])
func.func @test_change_mem_plan_order(%arg0: memref<32x256xf32, #hivm.address_space<gm>>, %arg1: memref<128x256xf32, #hivm.address_space<gm>>, %arg2: memref<16x256xf32, #hivm.address_space<gm>>,  %arg3: memref<64x256xf32, #hivm.address_space<gm>>) {
  %cst_0 = arith.constant 0x00000000 : f32
  %alloc = memref.alloc() : memref<16x256xf32, #hivm.address_space<ub>>
  hivm.hir.vbrc ins(%cst_0 : f32) outs(%alloc : memref<16x256xf32, #hivm.address_space<ub>>)
  %alloc_0 = memref.alloc() : memref<128x256xf32, #hivm.address_space<ub>>
  hivm.hir.vbrc ins(%cst_0 : f32) outs(%alloc_0 : memref<128x256xf32, #hivm.address_space<ub>>)
  hivm.hir.debug {debugtype = "print", hex = false, prefix = " %alloc: ", tcoretype = #hivm.tcore_type<CUBE_OR_VECTOR>} %alloc : memref<16x256xf32, #hivm.address_space<ub>>
  %alloc_1 = memref.alloc() : memref<48x256xf32, #hivm.address_space<ub>>
  hivm.hir.vbrc ins(%cst_0 : f32) outs(%alloc_1 : memref<48x256xf32, #hivm.address_space<ub>>)
  hivm.hir.debug {debugtype = "print", hex = false, prefix = " %alloc_1: ", tcoretype = #hivm.tcore_type<CUBE_OR_VECTOR>} %alloc_1 : memref<48x256xf32, #hivm.address_space<ub>>
  %alloc_2 = memref.alloc() : memref<48x256xf32, #hivm.address_space<ub>>
  hivm.hir.vbrc ins(%cst_0 : f32) outs(%alloc_2 : memref<48x256xf32, #hivm.address_space<ub>>)
  hivm.hir.debug {debugtype = "print", hex = false, prefix = " %alloc_2: ", tcoretype = #hivm.tcore_type<CUBE_OR_VECTOR>} %alloc_2 : memref<48x256xf32, #hivm.address_space<ub>>
  hivm.hir.debug {debugtype = "print", hex = false, prefix = " %alloc_0: ", tcoretype = #hivm.tcore_type<CUBE_OR_VECTOR>} %alloc_0 : memref<128x256xf32, #hivm.address_space<ub>>
  return
}
