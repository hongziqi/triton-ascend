// RUN: bishengir-opt --insert-memory-semantic-for-simtvf -split-input-file %s | FileCheck %s

// CHECK: %[[UB:.*]] = memref.alloc() : memref<8xf32>
// CHECK-NEXT: scope.scope
// CHECK-NEXT:   %[[REINTERPRETCAST1:.*]] = memref.reinterpret_cast %[[ARG1:.*]] to offset: [0], sizes: [8], strides: [1] : memref<?xi64> to memref<8xi64, strided<[1]>>
// CHECK-NEXT:   hivm.hir.load ins(%[[REINTERPRETCAST1]] : memref<8xi64, strided<[1]>>) outs(%[[ALLOC:.*]] : memref<8xi64>) eviction_policy = <EvictFirst>
// CHECK-NEXT:   %[[TOTENSOR:.*]] = bufferization.to_tensor %[[ALLOC]] restrict writable : memref<8xi64>
// CHECK-NEXT:   %[[EMPTY:.*]] = tensor.empty() : tensor<8xf32>
// CHECK-NEXT:   %[[GATHERLOAD:.*]] = hivm.hir.gather_load ins(%[[ARG2:.*]] : memref<?xf32>, %[[TOTENSOR]] : tensor<8xi64>, %[[c:.*]] : i32) outs(%[[EMPTY]] : tensor<8xf32>) {cache = #hivm.cache_modifier<none>, evict = #hivm.eviction_policy<EvictLast>, isVolatile = false} -> tensor<8xf32>
// CHECK-NEXT:   hivm.hir.local_store ins(%[[UB]] : memref<8xf32>, %[[GATHERLOAD]] : tensor<8xf32>)
// CHECK-NEXT:   scope.return
// CHECK:   %[[TENSOR:.*]] = bufferization.to_tensor %[[UB]] restrict : memref<8xf32>
// CHECK-NEXT: %[[REINTERPRETCAST2:.*]] = memref.reinterpret_cast %[[ARG3:.*]] to offset: [0], sizes: [8], strides: [1] : memref<?xf32> to memref<8xf32, strided<[1]>>
// CHECK-NEXT: hivm.hir.store ins(%[[TENSOR]] : tensor<8xf32>) outs(%[[REINTERPRETCAST2]] : memref<8xf32, strided<[1]>>)
module {
  func.func @simple_indirect_load_kernel(%arg0: memref<?xi8>, %arg1: memref<?xi8>, %arg2: memref<?xf32>, %arg3: memref<?xi64>, %arg4: memref<?xf32>, %arg5: i32, %arg6: i32, %arg7: i32) {
    %c1_i32 = arith.constant 1 : i32
    hivm.hir.set_ctrl false at ctrl[60]
    hivm.hir.set_ctrl true at ctrl[48]
    %0 = arith.muli %arg5, %arg6 : i32
    %1 = arith.muli %0, %arg7 : i32
    annotation.mark %1 {logical_block_num} : i32
    %alloc = memref.alloc() : memref<8xi64>
    %2 = scope.scope : () -> tensor<8xf32> {
      %reinterpret_cast_0 = memref.reinterpret_cast %arg3 to offset: [0], sizes: [8], strides: [1] : memref<?xi64> to memref<8xi64, strided<[1]>>
      hivm.hir.load ins(%reinterpret_cast_0 : memref<8xi64, strided<[1]>>) outs(%alloc : memref<8xi64>) eviction_policy = <EvictFirst>
      %3 = bufferization.to_tensor %alloc restrict writable : memref<8xi64>
      %4 = tensor.empty() : tensor<8xf32>
      %5 = hivm.hir.gather_load ins(%arg2 : memref<?xf32>, %3 : tensor<8xi64>, %c1_i32 : i32) outs(%4 : tensor<8xf32>) {cache = #hivm.cache_modifier<none>, evict = #hivm.eviction_policy<EvictLast>, isVolatile = false} -> tensor<8xf32>
      scope.return %5 : tensor<8xf32>
    } {no_inline, outline, hivm.vf_mode = #hivm.vf_mode<SIMT>}
    %reinterpret_cast = memref.reinterpret_cast %arg4 to offset: [0], sizes: [8], strides: [1] : memref<?xf32> to memref<8xf32, strided<[1]>>
    hivm.hir.store ins(%2 : tensor<8xf32>) outs(%reinterpret_cast : memref<8xf32, strided<[1]>>)
    return
  }
}

// -----

// CHECK: %[[TOMEMREF:.*]] = bufferization.to_memref %[[TENSOR:.*]] : memref<8xf32>
// CHECK-NEXT: scope.scope
// CHECK-NEXT:   %[[REINTERPRETCAST:.*]] = memref.reinterpret_cast %[[ARG1:.*]] to offset: [0], sizes: [8], strides: [1] : memref<?xi64> to memref<8xi64, strided<[1]>>
// CHECK-NEXT:   hivm.hir.load ins(%[[REINTERPRETCAST]] : memref<8xi64, strided<[1]>>) outs(%[[ALLOC:.*]] : memref<8xi64>) eviction_policy = <EvictFirst>
// CHECK-NEXT:   %[[TOTENSOR1:.*]] = bufferization.to_tensor %[[ALLOC]] restrict writable : memref<8xi64>
// CHECK-NEXT:   %[[LOAD:.*]] = hivm.hir.local_load ins(%[[TOMEMREF]] : memref<8xf32>) -> tensor<8xf32>
// CHECK-NEXT:   hivm.hir.scatter_store ins(%[[TOTENSOR1]] : tensor<8xi64>, %[[LOAD]] : tensor<8xf32>, %[[c:.*]] : i32) outs(%[[ARG2:.*]] : memref<?xf32>) {cache = #hivm.cache_modifier<none>, evict = #hivm.eviction_policy<EvictLast>}
// CHECK-NEXT:   scope.return
module {
  func.func @simple_indirect_store_kernel(%arg0: memref<?xi8>, %arg1: memref<?xi8>, %arg2: memref<?xf32>, %arg3: memref<?xi64>, %arg4: memref<?xf32>, %arg5: i32, %arg6: i32, %arg7: i32) {
    %c1_i32 = arith.constant 1 : i32
    hivm.hir.set_ctrl false at ctrl[60]
    hivm.hir.set_ctrl true at ctrl[48]
    %0 = arith.muli %arg5, %arg6 : i32
    %1 = arith.muli %0, %arg7 : i32
    annotation.mark %1 {logical_block_num} : i32
    %alloc = memref.alloc() : memref<8xi64>
    %reinterpret_cast = memref.reinterpret_cast %arg4 to offset: [0], sizes: [8], strides: [1] : memref<?xf32> to memref<8xf32, strided<[1]>>
    %alloc_0 = memref.alloc() : memref<8xf32>
    hivm.hir.load ins(%reinterpret_cast : memref<8xf32, strided<[1]>>) outs(%alloc_0 : memref<8xf32>) eviction_policy = <EvictFirst>
    %2 = bufferization.to_tensor %alloc_0 restrict writable : memref<8xf32>
    scope.scope : () -> () {
      %reinterpret_cast_1 = memref.reinterpret_cast %arg3 to offset: [0], sizes: [8], strides: [1] : memref<?xi64> to memref<8xi64, strided<[1]>>
      hivm.hir.load ins(%reinterpret_cast_1 : memref<8xi64, strided<[1]>>) outs(%alloc : memref<8xi64>) eviction_policy = <EvictFirst>
      %3 = bufferization.to_tensor %alloc restrict writable : memref<8xi64>
      hivm.hir.scatter_store ins(%3 : tensor<8xi64>, %2 : tensor<8xf32>, %c1_i32 : i32) outs(%arg2 : memref<?xf32>) {cache = #hivm.cache_modifier<none>, evict = #hivm.eviction_policy<EvictLast>}
      scope.return
    } {no_inline, outline, hivm.vf_mode = #hivm.vf_mode<SIMT>}
    return
  }
}

// -----

// CHECK: hivm.hir.local_store
// CHECK-NEXT: hivm.hir.local_store
// CHECK-NEXT: hivm.hir.local_store
// CHECK-NEXT: scope.return
module {
  func.func @scope_with_multi_return(%arg0: memref<?xi8>, %arg1: memref<?xi8>, %arg2: memref<?xf32>, %arg3: memref<?xi64>, %arg4: memref<?xf32>, %arg5: i32, %arg6: i32, %arg7: i32) {
    %c1_i32 = arith.constant 1 : i32
    hivm.hir.set_ctrl false at ctrl[60]
    hivm.hir.set_ctrl true at ctrl[48]
    %0 = arith.muli %arg5, %arg6 : i32
    %alloc = memref.alloc() : memref<8xi64>
    %2:3 = scope.scope : () -> (tensor<8xf32>, tensor<8xf32>, tensor<8xf32>) {
      %reinterpret_cast_0 = memref.reinterpret_cast %arg3 to offset: [0], sizes: [8], strides: [1] : memref<?xi64> to memref<8xi64, strided<[1]>>
      hivm.hir.load ins(%reinterpret_cast_0 : memref<8xi64, strided<[1]>>) outs(%alloc : memref<8xi64>) eviction_policy = <EvictFirst>
      %3 = bufferization.to_tensor %alloc restrict writable : memref<8xi64>
      %4 = tensor.empty() : tensor<8xf32>
      %5 = hivm.hir.gather_load ins(%arg2 : memref<?xf32>, %3 : tensor<8xi64>, %c1_i32 : i32) outs(%4 : tensor<8xf32>) {cache = #hivm.cache_modifier<none>, evict = #hivm.eviction_policy<EvictLast>, isVolatile = false} -> tensor<8xf32>
      scope.return %5, %5, %5 : tensor<8xf32>, tensor<8xf32>, tensor<8xf32>
    } {no_inline, outline, hivm.vf_mode = #hivm.vf_mode<SIMT>}
    return
  }
}



// -----

// Test case for out-of-scope reference in nested region
// CHECK: %[[TOMEMREF:.*]] = bufferization.to_memref %[[TENSOR:.*]] : memref<8xi64>
// CHECK-NEXT: memref.alloc()
// CHECK-NEXT: scope.scope
// CHECK-NEXT: tensor.empty()
// CHECK-NEXT: scf.for
// CHECK:      %[[LOCALLOAD:.*]] = hivm.hir.local_load ins(%[[TOMEMREF]] : memref<8xi64>) -> tensor<8xi64>
// CHECK-NEXT: %[[GATHERLOAD:.*]] = hivm.hir.gather_load ins(%{{.*}} : memref<?xf32>, %[[LOCALLOAD]] : tensor<8xi64>, %{{.*}} : i32)
// CHECK:   hivm.hir.local_store
// CHECK:   scope.return
module {
  func.func @scope_with_nested_region(%arg0: memref<?xi8>, %arg1: memref<?xi8>, %arg2: memref<?xf32>, %arg3: memref<?xi64>, %arg4: memref<?xf32>, %arg5: i32, %arg6: i32, %arg7: i32) {
    %c1_i32 = arith.constant 1 : i32
    %c1_index = arith.constant 1 : index
    %c4_index = arith.constant 4 : index
    hivm.hir.set_ctrl false at ctrl[60]
    hivm.hir.set_ctrl true at ctrl[48]
    %0 = arith.muli %arg5, %arg6 : i32
    annotation.mark %0 {logical_block_num} : i32
    // Create an i64 tensor outside the scope (for gather_load index)
    %alloc = memref.alloc() : memref<8xi64>
    %reinterpret_cast = memref.reinterpret_cast %arg3 to offset: [0], sizes: [8], strides: [1] : memref<?xi64> to memref<8xi64, strided<[1]>>
    hivm.hir.load ins(%reinterpret_cast : memref<8xi64, strided<[1]>>) outs(%alloc : memref<8xi64>) eviction_policy = <EvictFirst>
    %tensor_outside = bufferization.to_tensor %alloc restrict writable : memref<8xi64>
    // Use the out-of-scope tensor inside nested region (scf.for)
    %1 = scope.scope : () -> tensor<8xf32> {
      %empty = tensor.empty() : tensor<8xf32>
      scf.for %i = %c1_index to %c4_index step %c1_index {
        %idx = arith.index_cast %i : index to i32
        %gather = hivm.hir.gather_load ins(%arg2 : memref<?xf32>, %tensor_outside : tensor<8xi64>, %idx : i32) outs(%empty : tensor<8xf32>) {cache = #hivm.cache_modifier<none>, evict = #hivm.eviction_policy<EvictLast>, isVolatile = false} -> tensor<8xf32>
      }
      scope.return %empty : tensor<8xf32>
    } {no_inline, outline, hivm.vf_mode = #hivm.vf_mode<SIMT>}
    return
  }
}
